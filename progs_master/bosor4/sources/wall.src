C=DECK      WALLLIB
C
C  THIS IS THE WALL LIBRARY  (SOURCE FILE)
C
C
C
C=DECK      WALLS
C     OVERLAY(1,4)
      SUBROUTINE WALLS(BBB,Z,S,DS,ZAX)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
      COMMON/ALOFIT/INDSIG,II,ITOT,J,I,IBOUND,IPT,IRIGID,ISEG,I5I,KRING,
     1NWALL,NLTYPE,NRINGS,DSTOT,SEND,IPOS,IEND,IENDS,IMAX,ITOTL,LINTYP
      COMMON/NONEA/KRINGS,NPSTAT,NTSTAT,ARC(98),TT(600)
      COMMON/LOADHM/NLPOS(95),LAB(95),IDAB(95),ILPOS(12,95),PALL(6000)
      COMMON/SFLAG/ISTRES
      COMMON/STRSEG/ISTRSS(95)
      COMMON/TEMTUR/TEMP,DTEMP
      COMMON/SEGS/NSEG,M2,I5(95),I2,I2G
      COMMON/KCHNG/NNWALL(95),CCH(95)
      COMMON/PRMFIL/IFILE,IFILE2,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
C BEG APR 1992
      COMMON/ILVARS/ILVAR(11,95),ITREF(95),NLVAR(95),ITIO(95)
C END APR 1992
      CHARACTER*4 CHARAC
      DIMENSION BBB(10000), Z(1000),S(1000),ZAX(1000),DS(1000)
C     CALCULATION OF WALL STIFFNESS COEFFICIENTS AND THERMAL LOADS,ITH S
10    FORMAT(10I6)
      WRITE(IOUT,5)
    5 FORMAT(6X,'H',9X,'$ SHELL WALL CONSTRUCTION FOLLOWS...')
   20 CONTINUE
      CALL DATUM(IFILE2,340,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,341,1,2,NWALL,REALL,CHARAC,IOUT,0,1)
C BEG APR 1992
      IF (NWALL.LT.1.OR.NWALL.GT.9) THEN
       WRITE(6,*)' NWALL NOT IN PERMISSIBLE RANGE 1 TO 9'
C END APR 1992
       CALL ERREX
      ENDIF
C
      NNWALL(I) = NWALL
      IF (ISTRES.EQ.2) GO TO 30
      IF (NWALL.NE.2.AND.NWALL.NE.5.AND.NWALL.NE.4.AND.NWALL.NE.9)
     1    ISTRSS(I) = 0
   30 CONTINUE
      ILD = 0
      IF (ILPOS(12,I).NE.0) ILD = 4
      ILT = 1
      IF (ILPOS(12,I).NE.0) ILT = 4
      ILTIO = 39
      IF (ILPOS(12,I).NE.0) ILTIO = 41
      CALL WALLCF(I,NWALL,I5I,TT,ILT,BBB(22*I5I+IPOS),BBB(8*I5I+IPOS),
     1 BBB(ILD*I5I+IPOS),Z(ITOT),S(ITOT),TEMP,BBB(ILTIO*I5I+IPOS),NSEG,
     2BBB(23*I5I+IPOS),BBB(24*I5I+IPOS),BBB(25*I5I+IPOS),ZAX(ITOT),
     2 DS(ITOT),ITOT)
C BEG APR 1992
      CALL GASP(BBB(39*I5I+IPOS),I5I*4,1,ITIO(I))
C END APR 1992
      RETURN
      END
C=DECK      WALLCF
      SUBROUTINE WALLCF(II,NWALL,I5,TT,ILT,RAD,C,THERM,ZREF,S,TEMP,
     1TIO,NSEG,RADD,CUR1,CUR2,ZAXIAL,DS,ITOT)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C     CALLED FROM READIT, WHICH IS CALLED FROM MAIN.
C     THE WALL STIFFNESS COEFFICIENTS CIJ ARE CALCULATED FOR THE
C     CURRENT SEGMENT, II. SEVERAL TYPES OF SHELL WALLS MAY BE
C     INCLUDED IN THE ANALYSIS OF A COMPOSITE SHELL STRUCTURE, AS LONG
C     AS THE MODEL REMAINS AXISYMMETRIC.
      COMMON/OUTP/NPRT
      COMMON/IDZREF/IZREF(95)
      COMMON/SFLAG/MONOQ
      COMMON/AXIALZ/ZAX(100)
      COMMON/CMAXS/CMAX
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/SHMASS/SMPA
      COMMON/WAVES/N0,N,NMIN,NMAX,INCR
      COMMON/INSTAB/INDIC
C  FOLLOWING COMMON BLOCKS ADDED 17 AUG 1985 FOR DYNAMIC REACTION..
      COMMON/GLOBAL/IDZ,IZRING(98),ISAVE(20),INOTE
       COMMON/DYNAM/FX(2),FY(2),FZ(2),MX(2),MY(2),MZ(2),
     1              ICM,IZ,M,ZCM,IDYN(2)
      COMMON/RNGGEO/RC(98),AREA(98),ER(98),IY(98),IX(98),IXY(98),IP(98)
       REAL MX,MY,MZ,ICM,IZ,M,IX,IY,IXY,IP
      COMMON/RMASS/RM(98)
      COMMON/IRNGZ/KRNG
      COMMON/COMNOD/NODES(100),NODALL(100),NODTOT
C  END 17 AUG INSERT
      DIMENSION ZREF(*),TT(I5,6),C(I5,14),THERM(I5,4),TIO(I5,2)
C BEG APR 1992
      DIMENSION THICK(1000)
C END APR 1992
      DIMENSION  ZAXIAL(*),DS(*)
      DIMENSION S(*),RAD(*),RADD(*),CUR1(*),CUR2(*),TH(100)
      COMMON/INDAT/INFILE
      CHARACTER*4 ANSOUT
      LOGICAL ANSL1
      COMMON/PRMFIL/IFILE,IFILE2,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
C BEG MARCH 1990
      COMMON/ZZZGLB/ZGLOBL
C END MARCH 1990
      CALL GASP(ZREF,I5,1,IZREF(II))
      WRITE (IFILE4,10)II
10    FORMAT(//40X, 35HPHYSICAL PROPERTIES OF SEGMENT NO.          I3)
      J = 1
      DO 20 I=1,I5
20    ZAX(I) = ZAXIAL(I)
   25 CONTINUE
      WALMAS = 0
      IF (II.EQ.1) THEN
         DO 26 I = 1,100
   26    NODALL(I) = 1
         CALL MOVER(0,0,NODES,1,100)
         NODTOT = 0
      ENDIF
C
      DO 190 I=1,I5
      Z = ZREF(I)
      T1 = TT(I,ILT)
      T2 = TT(I,ILT+1)
      T3 = TT(I,ILT+2)
C BEG OCT 2004
C     IF (I.EQ.1) THEN
C        WRITE(IFILE4,'(A,/,A,3I5,/,A,1P,4E12.4)')
C    1' ***** EXTRA OUTPUT FOR THERMAL STUFF *****',
C    1' ISEG,I,ILT=',II,I,ILT,
C    1' Z,T1,T2,T3=',Z,T1,T2,T3
C     ENDIF
C END OCT 2004
C
C     GO TO APPROPRIATE CFB TO FIND THE CIJ FOR THIS SEGMENT.
C
C BEG APR 1992
      GO TO (50,60,70,80,90,100,110,120,125),NWALL
C END APR 1992
C     READ IN CIJ FOR ARBITRARY SHELL. PROPERTIES MUST BE CONSTANT
C     ALONG MERIDIAN
50    CALL CFB1(I,RAD,NWALL,Z,T,I5,S,C,RADD)
      GO TO 130
C     MONOCOQUE SHELL WITH CONSTANT OR VARIABLE THICKNESS
60    CALL CFB2(TH,II,I,RAD,NWALL,ZREF,I5,S,THERM,T1,T2,T3,J,TIO,
     1C,RADD,CUR1,CUR2)
      GO TO 130
C     MONOCOQUE SHELL WITH SKEWED STIFFENERS. AVOID USING IF POSSIBLE
C     AS THIS SUBROUTINE IS NOT CHECKED OUT
70    CALL CFB3(I)
      GO TO 130
C     LAYERED LAMINATED SHELL (ROUTINE CFB4 INCLUDED ON 21 AUG, 1985)
80    CALL CFB4(II,I,RAD,NWALL,Z,I5,S,C,RADD,THERM,T1,T2,T3,TIO)
      GO TO 130
C     LAYERED ORTHOTROPIC SHELL WHERE LAYERS MAY BE OF CONSTANT OR OF
C     VARIABLE THICKNESS
90    CALL CFB5(I,RAD,NWALL,Z,I5,THERM,T1,T2,T3,J,S,C,RADD,II,TIO)
      GO TO 130
C     CORRUGATED SHELL WITHOUT SMOOTH SKIN
100   CALL CFB6(I,RAD,NWALL,Z,I5,S,C,RADD)
      GO TO 130
C     SEMI-SANDWICH CORRUGATED SHELL. SKIN MAY VARY IN THICKNESS
110   CALL CFB7(I,RAD,NWALL,Z,S,I5,II,TIO,S,C,RADD)
      GO TO 130
C     LAYERED ORTHOTROPIC SHELL WITH TEMPERATURE-DEPENDENT MATERIAL
C     PROPERTIES. LAYERS MAY VARY IN THICKNESS. BE SURE TEMPERATURE
C     DEPENDENCE IS AXISYMMETRIC OR SOLUTION MAY NOT MAKE SENSE.
120   CALL CFB8(I,RAD,NWALL,Z,I5,THERM,T1,T2,T3,TEMP,J,S,C,RADD)
C BEG APR 1992
      GO TO 130
C     LAYERED COMPOSITE WITH SOME VARIABLE THICKNESS LAYERS...
  125 CALL CFB9(II,I,RAD,NWALL,Z,I5,S,C,RADD,THERM,T1,T2,T3,TIO,
     1          THICK)
C END APR 1992
130   CONTINUE
      C(I,1) = C11
      C(I,2) = C12
      C(I,3) = C14
      C(I,4) = C15
      C(I,5) = C22
      C(I,6) = C24
      C(I,7) = C25
      C(I,8) = C44
      C(I,9) = C45
      C(I,10) = C55
      C(I,11) = C33
      C(I,12) = C36
      C(I,13) = C66
C     SHELL MASS/SURFACEAREA
      C(I,14) = SMPA
C
      IF (C11.EQ.0.) THEN
         WRITE(6,*) ' ZERO STIFFNESS C11 DETECTED AT NODAL POINT', I
         REWIND IFILE2
         GO TO 25
      ENDIF
C
C  FOLLOWING STATEMENTS ADDED 17 AUGUST, 1985 FOR DYNAMIC REACTION..
C
      IF (II.EQ.1.AND.I.EQ.1) KRNG = 1
      FACT = 0.
      IF ((ITOT+I-1).EQ.IZRING(KRNG)) FACT = 1.
C
C  TOTAL MASS, MASS MOMENT OF INERTIA, CENTER OF MASS
C
      H = DS(I)
      IF (I.EQ.1.OR.I.EQ.I5) H = DS(I)/2.
      RDS = RAD(I)*H
      PI = 3.1415927
C BEG MARCH 1990
      ZGLBL = ZAX(I) + ZGLOBL
      ICM = ICM + PI*RDS*SMPA*(RAD(I)**2 +2.*ZGLBL**2)
     1 + FACT*PI*RM(KRNG)*RC(KRNG)*AREA(KRNG)*
     1        (RC(KRNG)**2 +2.*ZGLBL**2)
      M   = M +2.*PI*(RDS*SMPA +FACT*RC(KRNG)*AREA(KRNG)*RM(KRNG))
      IZ = IZ +2.*PI*(RDS*SMPA*RAD(I)**2 +FACT*AREA(KRNG)*RM(KRNG)*
     1        RC(KRNG)**3)
      ZCM = ZCM + 2.*PI*(RDS*SMPA*ZGLBL +FACT*RC(KRNG)*AREA(KRNG)*
     1        RM(KRNG)*ZGLBL)
C END MARCH 1990
C
      IF ((ITOT+I-1).EQ.IZRING(KRNG)) KRNG = KRNG + 1
C
C  END OF 17 AUG 1985 DYNAMIC REACTION STATEMENTS
C
      IF (I.GT.1) GO TO 160
      REWIND IFILE3
      CALL DATUM(IFILE3,607,1,1,INT,REALL,ANSOUT,IOUT,0,1)
      IF (ANSL1('N',ANSOUT,INFILE)) GO TO 180
      WRITE (IFILE4,140)II
140   FORMAT(//76H  WALL STIFFNESS COEFFICIENTS,CIJ, AND MASS/AREA, SMPA
     1, FOR SEGMENT NUMBER        I3)                  
      WRITE (IFILE4,150)
150   FORMAT(  130H   C11      C12      C14      C15      C22      C24  
     1    C25      C44      C45      C55      C33      C36      C66   SM    
     2PA                         /)                          
160   CONTINUE
      IF (ANSL1('N',ANSOUT,INFILE)) GO TO 180
      WRITE(IFILE4,170) C11,C12,C14,C15,C22,C24,C25,C44,
     1 C45,C55,C33,C36,C66,SMPA
170   FORMAT(1P14E9.2)
180   CONTINUE
      IF (II.EQ.1.AND.I.EQ.1) CMAX = ABS(C11)
      CMAX = AMAX1(ABS(C11),CMAX)
      WALMAS = WALMAS + C(I,14)*DS(I)*RAD(I)
190   CONTINUE
C
      WALMAS = 2.*3.1415927*WALMAS
      WRITE(IFILE4,200) II, WALMAS
  200 FORMAT(//' SHELL WALL MASS OF SEGMENT NO.',I3,' = ',1PE12.4/
     1         ' (DISCRETE RINGS NOT INCLUDED)'//)
C
C  FOLLOWING STATEMENTS ADDED 17 AUG, 1985 (DYNAMIC REACTION)
C
      IF (II.NE.NSEG) GO TO 400
      IF (M.GT.0.) ZCM = ZCM/M
      ICM = ICM - M*ZCM**2
      WRITE(IFILE4,250) ZCM
  250 FORMAT(//' ***************************************'/
     1' TOTAL FORCES AND MOMENTS FROM APPLIED LOADS. THE FORCES AND'/
     1' MOMENTS OCCUR AT THE ORIGIN OF THE GLOBAL X,Y,Z AXIS SYSTEM,'/
     1' WHICH IS LOCATED AT AXIAL STATION Z=0. NOTE THAT THE TOTAL'/
     1' FORCES AND MOMENTS ARE NOT APPLIED AT THE C.G. OF THE'/
     1' STRUCTURE, WHICH IS AT AXIAL LOCATION ZCM=',1PE12.4,', BUT'/
     1' THEY ARE APPLIED AT AXIAL STATION Z = 0.'//
     1' NOTE: IN ORDER FOR THE MASS PROPERTIES AND DYNAMIC REACTIONS'/
     1' TO BE PROPERLY COMPUTED, THE GEOMETRY OF EVERY SEGMENT IN'/
     1' THE ENTIRE STRUCTURE MUST BE EXPRESSED IN TERMS OF THE'/
     1' GLOBAL AXIAL COORDINATE, Z.  THAT IS, YOU MUST HAVE PROVIDED'/
     1' AS INPUT FOR EACH SEGMENT THE GLOBAL Z COORDINATES.')
      WRITE(IFILE4,260) FX(1),FY(1),FZ(1),MX(1),MY(1),MZ(1)
  260 FORMAT(/' TOTAL FORCES FROM LOAD SET A...'/
     1'  X-DIRECTION (THETA=  0 DEG.), FX(1) =             ',1PE12.4/
     1'  Y-DIRECTION (THETA=270 DEG.), FY(1) =             ',1PE12.4/
     1'  Z-DIRECTION (AXIAL UPWARD)  , FZ(1) =             ',1PE12.4//
     1' TOTAL MOMENTS FROM LOAD SET A...'/
     1'  ABOUT X-AXIS                , MX(1) =             ',1PE12.4/
     1'  ABOUT Y-AXIS                , MY(1) =             ',1PE12.4/
     1'  ABOUT Z-AXIS                , MZ(1) =             ',1PE12.4/)
      WRITE(IFILE4,265) FX(2),FY(2),FZ(2),MX(2),MY(2),MZ(2)
  265 FORMAT(/' TOTAL FORCES FROM LOAD SET B...'/
     1'  X-DIRECTION (THETA=  0 DEG.), FX(2) =             ',1PE12.4/
     1'  Y-DIRECTION (THETA=270 DEG.), FY(2) =             ',1PE12.4/
     1'  Z-DIRECTION (AXIAL)         , FZ(2) =             ',1PE12.4//
     1' TOTAL MOMENTS FROM LOAD SET B...'/
     1'  ABOUT X-AXIS                , MX(2) =             ',1PE12.4/
     1'  ABOUT Y-AXIS                , MY(2) =             ',1PE12.4/
     1'  ABOUT Z-AXIS                , MZ(2) =             ',1PE12.4/)
      WRITE(IFILE4,300) M,IZ,ICM,ZCM
  300 FORMAT(/' TOTAL MASS, LOCATION OF C.G., MASS MOMENTS OF INERTIA'/
     1'   TOTAL MASS, M (e.g. lb-sec**2/in)          M =   ',1PE12.4/
     1'   POLAR MASS MOMENT OF INERTIA,             IZ =   ',1PE12.4/
     1'   MASS MOMENT OF INERTIA ABOUT C.G.        ICM =   ',1PE12.4/
     1'   AXIAL POSITION OF THE C.G.               ZCM =   ',1PE12.4/)
C
  400 CONTINUE
C
C END OF 17 AUG 1985 STATEMENTS.
      RETURN
      END
C=DECK      CFB1
      SUBROUTINE CFB1(I,RAD,NWALL,Z,TD,I5,S,C,RADD)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C     CALLED FROM WALLCF, WHICH IS CALLED FROM READIT, WHICH IS CALLED
C     FROM MAIN. CAUSES CIJ TO BE READ IN DIRECTLY OR MODIFIES SHELL
C     WALL STIFFNESS COEFFICIENTS CIJ TO ACCOUNT FOR MERIDIONAL AND/OR
C     CIRCUMFERENTIAL STIFFENERS WHICH ARE SMEARED OUT ACCORDING TO
C     THE BARUCH-SINGER THEORY.
C
C     ALSO CALLED FROM CFB2,CFB4,CFB5,CFB6,CFB7, AND CFB8.
C
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/PRMFIL/IFILE,IFILE2,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
      COMMON/INDAT/INFILE
      CHARACTER*4 ANSWER,ANSCON,ANSREC,CHARAC
      LOGICAL ANSL1
10    FORMAT(10I6)
      COMMON/SHMASS/SMPA
      DIMENSION RAD(100),S(100),C(I5,14),ISTATN(50),T(50),H(50),X(50)
      DIMENSION A(50),XI(50),XJ(50),D(50),RADD(100)
      COMMON/AXIALZ/ZAXIAL(100)
20    FORMAT (6E12.8)
30    FORMAT (/38H STIFFNESS COEFFICIENTS READ AS INPUT./5H C11=E15.8,
     15X,4HC12=E15.8,5X,4HC14=E15.8,5X,4HC15=E15.8/5H C22=E15.8,5X,
     24HC24=E15.8,5X,4HC25=E15.8,5X,4HC33=E15.8/5H C44=E15.8,5X,
     34HC45=E15.8,5X,4HC55=E15.8,5X,4HC66=E15.8,5X,4HC36= E15.8)
40    FORMAT (/23H NO STRINGERS OR RINGS.)
50    FORMAT (/36H ANALYSIS IS FOR AN ABRITRARY SHELL.)
      IF (NWALL.NE.1) GO TO 70
      IF (I.NE.1) GO TO 60
      WRITE (IFILE4,50)
C
C     READ STIFFNESS COEFFICIENTS FOR ARBITRARY SHELL.
C
      CALL DATUM(IFILE2,350,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,351,1,1,INT,SMPAD,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,352,1,1,INT,D11,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,353,1,1,INT,D12,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,354,1,1,INT,D14,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,355,1,1,INT,D15,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,356,1,1,INT,D22,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,357,1,1,INT,D24,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,358,1,1,INT,D25,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,359,1,1,INT,D33,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,360,1,1,INT,D44,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,361,1,1,INT,D45,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,362,1,1,INT,D55,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,363,1,1,INT,D66,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,364,1,1,INT,D36,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,365,1,2,INT,ANRS,CHARAC,IOUT,0,1)
C
      WRITE(IFILE4,30)D11,D12,D14,D15,D22,D24,D25,D33,D44,
     1 D45,D55,D66,D36
60    NRS=ANRS
      SMPA = SMPAD
      C11 = D11
      C12 = D12
      C14 = D14
      C15 = D15
      C22 = D22
      C24 = D24
      C25 = D25
      C33 = D33
      C36 = D36
      C44 = D44
      C45 = D45
      C55 = D55
      C66 = D66
      IF (NRS.NE.0) GO TO 70
      IF (I.EQ.1) WRITE (6,40)
      RETURN
C
C     MODIFY STIFFNESS COEFFICIENTS FOR EFFECT OF RINGS AND STRINGERS.
C
70    CONTINUE
      IF (I.NE.1) GO TO 600
C
C     IRECT1 = 1   RECTANGULAR STRINGERS
C     IRECT1 = 0  NON-RECTANGULAR STRINGERS
C     IRECT2 = 1  RECTANGULAR RINGS
C     IRECT2 = 0  NON-RECTANGULAR RINGS
C     IVAR1 = 1  STRINGER CROSS-SECTION PROPERTIES VARY ALONG MERIDIAN
C     IVAR2 = 1      RING CROSS-SECTION PROPERTIES VARY ALONG MERIDIAN
C     IVAR1 = 0  STRINGER CROSS-SECTION CONSTANT ALONG MERIDIAN
C     IVAR2 = 0      RING CROSS-SECTION CONSTANT ALONG MERIDIAN
C
      WRITE(IOUT,2)
    2 FORMAT(6X,'H',9X,'$ SMEARED STIFFENER INPUT FOLLOWS...')
      CALL DATUM(IFILE2,500,0,0,INT,REALL,CHARAC,IOUT,0,1)
      IRECT1 = 1
      IRECT2 = 1
      IVAR1  = 0
      IVAR2  = 0
      N1 = 1
      K1 = 0
      E1 = 0.
      U1 = 0.
      STIFMD = 0.
      T1 = 0.
      H1 = 0.
      K2 = 0
      E2 = 0.
      U2 = 0.
      RGMD=0.
      D2 = 1.
      T2 = 0.
      H2 = 0.
C
      CALL DATUM(IFILE2,501,1,1,INT,REALL,ANSWER,IOUT,0,1)
      IF (ANSL1('N',ANSWER,INFILE)) GO TO 130
C
      CALL DATUM(IFILE2,502,1,1,N1,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,503,1,2,K1,REALL,CHARAC,IOUT,0,1)
      IF (K1.NE.0.AND.K1.NE.1) THEN
       WRITE(6,*)' K1 NOT IN PERMISSIBLE RANGE 0 TO 1'
       CALL ERREX
      ENDIF
      CALL DATUM(IFILE2,504,1,1,INT,E1,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,505,1,1,INT,U1,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,506,1,1,INT,STIFMD,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,509,1,1,INT,REALL,ANSCON,IOUT,0,1)
      IF (ANSL1('Y',ANSCON,INFILE)) GO TO 80
C
C    STRINGER CROSS SECTION VARIES IN THE MERIDIONAL DIRECTION..
C
      IVAR1 = 1
      CALL DATUM(IFILE2,510,1,2,NSTATN,REALL,CHARAC,IOUT,0,1)
      IF (NSTATN.LT.2.OR.NSTATN.GT.20) THEN
       WRITE(6,*)' NSTATN NOT IN PERMISSIBLE RANGE 2 TO 20'
       CALL ERREX
      ENDIF
      IPRM(2) = 511
      CALL STA(ISTATN,ZAXIAL,RAD,RADD,S,NSTATN,I5)
   80 CONTINUE
      CALL DATUM(IFILE2,514,1,1,INT,REALL,ANSREC,IOUT,0,1)
      IF (ANSL1('N',ANSREC,INFILE)) GO TO 90
C
C   STRINGER CROSS SECTIONS ARE RECTANGULAR...
C
      IF (ANSL1('Y',ANSCON,INFILE)) GO TO 85
C
C   RECTANGULAR CROSS SECTION OF STRINGER VARIES ALONG THE MERIDIAN..
C
      DO 83 K= 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,515,1,1,INT,T(K),CHARAC,IOUT,K,IPROMP)
   83 CONTINUE
      DO 84 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,516,1,1,INT,H(K),CHARAC,IOUT,K,IPROMP)
   84 CONTINUE
C
      CALL FINDZ(NSTATN,ISTATN,T,I5,C(1,1),S)
      CALL FINDZ(NSTATN,ISTATN,H,I5,C(1,2),S)
      GO TO 130
C
   85 CONTINUE
C
C   RECTANGULAR CROSS SECTION IS CONSTANT ALONG THE MERIDIAN...
C
      CALL DATUM(IFILE2,521,1,1,INT,T1,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,522,1,1,INT,H1,CHARAC,IOUT,0,1)
      GO TO 130
C
C
   90 CONTINUE
      IRECT1 = 0
C
C   STRINGER CROSS SECTIONS ARE NOT RECTANGULAR...
C
      IF (ANSL1('Y',ANSCON,INFILE)) GO TO 100
C
C   STRINGER CROSS SECTION VARIES ALONG THE MERIDIAN...
C
      DO 92 K = 1,NSTATN
      IPROMP =1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,517,1,2,INT,X(K),CHARAC,IOUT,K,IPROMP)
   92 CONTINUE
      DO 94 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,518,1,1,INT,A(K),CHARAC,IOUT,K,IPROMP)
   94 CONTINUE
      DO 96 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,519,1,2,INT,XI(K),CHARAC,IOUT,K,IPROMP)
   96 CONTINUE
      DO 98 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,520,1,1,INT,XJ(K),CHARAC,IOUT,K,IPROMP)
   98 CONTINUE
C
C     SMEARED STRINGER PROPERTIES FOLLOW
C     NSTATN = NO. OF MERIDIONAL STATIONS FOR WHICH STRINGER PROPERTIES
C              ARE TO BE READ IN
C     N1 = NO. OF STRINGERS
C     K1 = 1  EXTERNAL STRINGERS
C     K1 = 0  INTERNAL STRINGERS
C     E1 = STRINGER MODULUS
C     U1 = STRINGER POISSON RATIO
C     STIFMD = STRINGER MATERIAL MASS DENSITY
C     T1 = STRINGER THICKNESS
C     H1 = STRINGER HEIGHT
C     XS = DISTANCE FROM NEUTRAL SURFACE OF STRINGER TO CLOSEST SHELL
C              SURFACE.
C     A1 = CROSS-SECTION AREA OF STRINGER
C     XI1 = STRINGER MOMENT OF INERTIA ABOUT ITS CENTROID
C     XJ1 = STRINGER TORSIONAL CONSTANT, J
C
C
      CALL FINDZ(NSTATN,ISTATN,X,I5,C(1,1),S)
      CALL FINDZ(NSTATN,ISTATN,A,I5,C(1,2),S)
      CALL FINDZ(NSTATN,ISTATN,XI,I5,C(1,3),S)
      CALL FINDZ(NSTATN,ISTATN,XJ,I5,C(1,4),S)
C
      GO TO 130
C
  100 CONTINUE
C
C     NON-RECTANGULAR STRINGER CROSS SECTIONS ARE MERIDIONALLY CONSTANT.
C
      CALL DATUM(IFILE2,523,1,2,INT,XS,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,524,1,1,INT,A1,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,525,1,2,INT,XI1,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,526,1,1,INT,XJ1,CHARAC,IOUT,0,1)
C
130   CONTINUE
C
C
C     SMEARED RING PROPERTIES
C
C
C     K2 = 0  INTERNAL RINGS
C     K2 = 1  EXTERNAL RINGS
C     E2 =  RING MODULUS
C     U2 = RING POISSON RATIO
C     RGMD =  RING MASS DENSITY
C     D2 =  SPACING BETWEEN RINGS
C     T2 =  RING THICKNESS
C     H2 =  RING HEIGHT
C     NRINGS = NO. OF MERIDIONAL STATIONS FOR WHICH RING PROPERTIES
C              ARE READ IN
C     XR =  DISTANCE FROM NEUTRAL SURFACE OF RING TO CLOSEST SHELL
C              SURFACE
C     A2 =  RING CROSS-SECTION AREA
C     XI2 = RING MOMENT OF INERTIA ABOUT CENTROID
C     XJ2 = RING TORSION CONSTANT, J
C
      CALL DATUM(IFILE2,530,1,1,INT,REALL,ANSWER,IOUT,0,1)
      IF (ANSL1('N',ANSWER,INFILE)) GO TO 190
C
      CALL DATUM(IFILE2,531,1,2,K2,REALL,CHARAC,IOUT,0,1)
      IF (K2.NE.0.AND.K2.NE.1) THEN
        WRITE(6,*)' K2 NOT IN PERMISSIBLE RANGE 0 TO 1'
        CALL ERREX
      ENDIF
      CALL DATUM(IFILE2,532,1,1,INT,E2,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,533,1,1,INT,U2,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,534,1,1,INT,RGMD,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,535,1,1,INT,REALL,ANSCON,IOUT,0,1)
      IF (ANSL1('Y',ANSCON,INFILE)) GO TO 135
C
C    RING CROSS SECTION VARIES IN THE MERIDIONAL DIRECTION..
C
      IVAR2 = 1
      CALL DATUM(IFILE2,536,1,2,NSTATN,REALL,CHARAC,IOUT,0,1)
      IF (NSTATN.LT.2.OR.NSTATN.GT.20) THEN
        WRITE(6,*)' NSTATN NOT IN PERMISSIBLE RANGE 2 TO 20'
        CALL ERREX
      ENDIF
      IPRM(2) = 537
      CALL STA(ISTATN,ZAXIAL,RAD,RADD,S,NSTATN,I5)
  135 CONTINUE
      CALL DATUM(IFILE2,540,1,1,INT,REALL,ANSREC,IOUT,0,1)
      IF (ANSL1('N',ANSREC,INFILE)) GO TO 150
C
C   RING CROSS SECTIONS ARE RECTANGULAR...
C
      IF (ANSL1('Y',ANSCON,INFILE)) GO TO 140
C
C   RECTANGULAR CROSS SECTION OF STRINGER VARIES ALONG THE MERIDIAN..
C
      DO 136 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,541,1,1,INT,D(K),CHARAC,IOUT,K,IPROMP)
  136 CONTINUE
      DO 137 K= 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,542,1,1,INT,T(K),CHARAC,IOUT,K,IPROMP)
  137 CONTINUE
      DO 138 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,543,1,1,INT,H(K),CHARAC,IOUT,K,IPROMP)
  138 CONTINUE
C
      CALL FINDZ(NSTATN,ISTATN,D,I5,C(1,5),S)
      CALL FINDZ(NSTATN,ISTATN,T,I5,C(1,6),S)
      CALL FINDZ(NSTATN,ISTATN,H,I5,C(1,7),S)
      GO TO 190
C
  140 CONTINUE
C
C   RECTANGULAR CROSS SECTION IS CONSTANT ALONG THE MERIDIAN...
C
      CALL DATUM(IFILE2,549,1,1,INT,D2,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,550,1,1,INT,T2,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,551,1,1,INT,H2,CHARAC,IOUT,0,1)
      GO TO 190
C
C
  150 CONTINUE
      IRECT2 = 0
C
C   RING CROSS SECTIONS ARE NOT RECTANGULAR...
C
      IF (ANSL1('Y',ANSCON,INFILE)) GO TO 170
C
C   RING CROSS SECTION VARIES ALONG THE MERIDIAN...
C
      DO 152 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,544,1,2,INT,X(K),CHARAC,IOUT,K,IPROMP)
  152 CONTINUE
      DO 154 K = 1,NSTATN
      IPROMP =1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,545,1,1,INT,D(K),CHARAC,IOUT,K,IPROMP)
  154 CONTINUE
      DO 156 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,546,1,1,INT,A(K),CHARAC,IOUT,K,IPROMP)
  156 CONTINUE
      DO 158 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,547,1,2,INT,XI(K),CHARAC,IOUT,K,IPROMP)
  158 CONTINUE
      DO 160 K = 1,NSTATN
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,548,1,1,INT,XJ(K),CHARAC,IOUT,K,IPROMP)
  160 CONTINUE
C
      CALL FINDZ(NSTATN,ISTATN,X,I5,C(1,5),S)
      CALL FINDZ(NSTATN,ISTATN,D,I5,C(1,6),S)
      CALL FINDZ(NSTATN,ISTATN,A,I5,C(1,7),S)
      CALL FINDZ(NSTATN,ISTATN,XI,I5,C(1,8),S)
      CALL FINDZ(NSTATN,ISTATN,XJ,I5,C(1,9),S)
      GO TO 190
  170 CONTINUE
C
C     NON-RECTANGULAR RING CROSS SECTIONS ARE MERIDIONALLY CONSTANT.
C
      CALL DATUM(IFILE2,552,1,2,INT,XR,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,553,1,1,INT,D2,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,555,1,1,INT,A2,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,556,1,2,INT,XI2,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,557,1,1,INT,XJ2,CHARAC,IOUT,0,1)
C
190   CONTINUE
      IF (K1.EQ.1) GO TO 210
      WRITE (IFILE4,200)N1
200   FORMAT(// I10,19H INTERNAL STRINGERS ) 
      GO TO 230
210   WRITE (IFILE4,220)N1
220   FORMAT(// I10,19H EXTERNAL STRINGERS ) 
230   IF (K2.EQ.1) GO TO 250
      WRITE (IFILE4,240)
240   FORMAT(//15H INTERNAL RINGS  )
      GO TO 270
250   WRITE (IFILE4,260)
260   FORMAT(//15H EXTERNAL RINGS  )
270   WRITE (IFILE4,280)E1,U1,STIFMD
280   FORMAT(//29H STRINGER PROPERTIES-MODULUS= 1PE13.5,19H,  POISSON,S 
     1RATIO= 1PE13.5,16H,  MASS DENSITY=  1PE13.5)
      IF (IRECT1.EQ.0) GO TO 350
      WRITE (IFILE4,290)
290   FORMAT(//39H STRINGERS OF RECTANGULAR CROSS-SECTION  )
      IF (IVAR1.NE.0) GO TO 310
      WRITE (IFILE4,300)T1,H1
300   FORMAT(//23H STRINGER THICKNESS,T1= 1PE13.5,22H,  STRINGER HEIGHT,
     1H1=  1PE13.5)
      GO TO 430
310   WRITE (IFILE4,320)
320   FORMAT(//72H RECTANGULAR STRINGERS WITH THICKNESS AND HEIGHT VARIA
     1BLE ALONG MERIDIAN     //)
      DO 330 KK=1,I5
330   WRITE (IFILE4,340)KK,S(KK),C(KK,1),C(KK,2)
340   FORMAT(9H MESH PT= I3,12H, STATION,S= 1PE13.5,12H, THICKNESS= 1PE1
     13.5,10H, HEIGHT= 1PE13.5) 
      GO TO 430
350   WRITE (IFILE4,360)
360   FORMAT(//37H STRINGERS OF ARBITRARY CROSS-SECTION  )
      IF (IVAR1.NE.0) GO TO 380
      WRITE (IFILE4,370)XS,A1,XI1,XJ1
370   FORMAT(//69H DISTANCE,XS, FROM NEUTRAL AXIS OF STRINGER TO CLOSEST
     1SHELL SURFACE= 1PE13.5/1X,14HSTRINGER AREA= 1PE13.5,21H, MOMENT O 
     2F INERTIA= 1PE13.5,21H, TORSION CONSTANT,J= 1PE13.5)
      GO TO 430
380   WRITE (IFILE4,390)
390   FORMAT(//72H ARBITRARY STRINGERS WITH GEOMETRICAL PROPERTIES VARIA
     1BLE ALONG MERIDIAN  //)
      WRITE (IFILE4,400)
400   FORMAT(76H MESH PT.   STATION   HEIGHT(XS)  AREA(A1)   MOM. INERTI
     1A  TORSION CONSTANT  //) 
      DO 410 KK=1,I5
410   WRITE (IFILE4,420)KK,S(KK),C(KK,1),C(KK,2),C(KK,3),C(KK,4)
420   FORMAT(I6,1P5E15.5) 
430   WRITE (IFILE4,440)E2,U2,RGMD
440   FORMAT(///26H RING PROPERTIES- MODULUS= 1PE13.5,19H,  POISSON,S RA
     1TIO= 1PE13.5,16H,  MASS DENSITY= 1PE13.5) 
      IF (IRECT2.EQ.0) GO TO 510
      WRITE (IFILE4,450)
450   FORMAT(//35H RINGS OF RECTANGULAR CROSS-SECTION  ) 
      IF (IVAR2.NE.0) GO TO 470
      WRITE (IFILE4,460)D2,T2,H2
460   FORMAT(//17H RING SPACING,D2= 1PE13.5,21H,  RING THICKNESS,T2=    
     11PE13.5, 18H,  RING HEIGHT,H2= 1PE13.5)
      GO TO 590
470   WRITE (IFILE4,480)
480   FORMAT(//78H RECTANGULAR RINGS WITH SPACING, THICKNESS, AND HEIGHT
     1VARIABLE ALONG MERIDIAN  //)
      DO 490 KK=1,I5
490   WRITE (IFILE4,500)KK,S(KK),C(KK,5),C(KK,6),C(KK,7)
500   FORMAT(10H MESH PT.= I3,12H, STATION,S= 1PE13.5,10H, SPACING=     
     11PE13.5,13H,  THICKNESS= 1PE13.5,10H,  HEIGHT=  1PE13.5)
      GO TO 590
510   WRITE (IFILE4,520)
520   FORMAT(//33H RINGS OF ARBITRARY CROSS-SECTION  ) 
      IF (IVAR2.NE.0) GO TO 540
      WRITE (IFILE4,530)XR,D2,A2,XI2,XJ2
530   FORMAT(//65H DISTANCE,XR, FROM NEUTRAL AXIS OF RING TO CLOSEST SHE
     1LL SURFACE= 1PE13.5/1X,13HRING SPACING= 1PE13.5,13H,  RING AREA=  
     21PE13.5,21H,  MOMENT OF INERTIA= 1PE13.5,22H,  TORSION CONSTANT,J=
     31PE13.5)
      GO TO 590
540   WRITE (IFILE4,550)
550   FORMAT(//68H ARBITRARY RINGS WITH GEOMETRICAL PROPERTIES VARIABLE 
     1ALONG MERIDIAN  //)
      WRITE (IFILE4,560)
560   FORMAT(110H MESH PT.   STATION   HEIGHT(XR)   SPACING(D2)   AREA(A
     12)  MOM. INERTIA  TORSION CONSTANT                                
     2//)
      DO 570 KK=1,I5
570   WRITE(IFILE4,580)KK,S(KK),C(KK,5),C(KK,6),C(KK,7),C(KK,8),C(KK,9)
580   FORMAT(I6,1P6E15.5)
590   CONTINUE
      AN1 = N1
      IF (AN1.EQ.0.0) AN1 = 1.0
      IF (D2.EQ.0.0) D2 = 1.0
600   D1 = 2.*3.1415927*RAD(I)/AN1
      IF (IRECT1.EQ.0) GO TO 640
      IF (IVAR1.EQ.0) GO TO 610
      T1 = C(I,1)
      H1 = C(I,2)
610   A1 = H1*T1
      XI1 = T1*H1**3/12.
      XS = H1/2.
      IF (T1-H1) 620,620,630
620   XJ1 = T1**3*H1/3.
      GO TO 650
630   XJ1 = H1**3*T1/3.
      GO TO 650
640   IF (IVAR1.EQ.0) GO TO 650
      XS = C(I,1)
      A1 = C(I,2)
      XI1 = C(I,3)
      XJ1 = C(I,4)
650   IF (IRECT2.EQ.0) GO TO 690
      IF (IVAR2.EQ.0) GO TO 660
      D2 = C(I,5)
      T2 = C(I,6)
      H2 = C(I,7)
660   A2 = T2*H2
      XI2 = T2*H2**3/12.
      XR = H2/2.
      IF (T2-H2) 670,670,680
670   XJ2 = T2**3*H2/3.
      GO TO 700
680   XJ2 = H2**3*T2/3.
      GO TO 700
690   IF (IVAR2.EQ.0) GO TO 700
      XR = C(I,5)
      D2 = C(I,6)
      A2 = C(I,7)
      XI2 = C(I,8)
      XJ2 = C(I,9)
700   CONTINUE
      IF (K1.NE.0) GO TO 710
      X1 = XS + Z
      GO TO 720
710   X1 = -XS - TD + Z
720   IF (K2.NE.0) GO TO 730
      X2 = XR + Z
      GO TO 740
730   X2= -XR - TD + Z
740   CONTINUE
      IF (D1.EQ.0.0) D1 = 1.0
      C11=C11+E1*A1/D1
      C14=C14+(X1*E1*A1/D1)
      C22=C22+E2*A2/D2
      C25=C25+(X2*E2*A2/D2)
      C44=C44+(E1*XI1/D1+X1**2*E1*A1/D1)
      C55=C55+(E2*XI2/D2+X2**2*E2*A2/D2)
      C66=C66+0.25*(E1*XJ1/(2.*(1.+U1)*D1)+E2*XJ2/(2.*(1.+U2)*D2))
      SMPA = SMPA + STIFMD*A1/D1 + RGMD*A2/D2
      RETURN
      END
C=DECK      FINDTH
      SUBROUTINE FINDTH(Z1,Z2,T1,T2,T3,NUM,FOUT)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C     INTEGRAL OF TEMPERATURE THRU SHELL THICKNESS CALCULATED FROM
C     SIMPSON,S RULE. THIS SUBROUTINE CALLED FROM CFB2 AND CFB5, AND
C     APPLIES FOR SHELLS WHOSE WALL PROPERTIES ARE INDEPENDENT OF THE
C     TEMPERATURE.
C
      IC = NUM - 1
      T = Z2 - Z1
      DZ = T/FLOAT(IC)
      FOUT = 0.0
      Z = Z1
      DO 10 I=1,IC,2
      ZDZ = Z + DZ
      ZDZ2 = Z + 2.*DZ
C
C     TEMPERATURE PROFILE THRU THICKNESS DETERMINED BY FUNCT
C
      F1 =  FUNCT(T1,T2,T3,Z)
      F2 =  FUNCT(T1,T2,T3,ZDZ)
      F3 =  FUNCT(T1,T2,T3,ZDZ2)
      Z = ZDZ2
      FOUT = FOUT + (F1 + 4.*F2 + F3)*DZ/3.
10    CONTINUE
      RETURN
      END
C=DECK      FUNCT
C./      ADD   NAME=FUNCT
      FUNCTION FUNCT(T1,T2,T3,ZETA)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C     CALLED FROM FINDTH, WHICH IS CALLED FROM CFB2,CFB5,.ALSO CALLED
C     FROM STIFF, WHICH IS CALLED FROM CFB8. GIVEN T1,T2,T3 AS INPUT
C     VARIABLES, FUNCT GIVES THE TEMPERATURE DISTRIBUTION THRU THE
C     THICKNESS OF THE SHELL WALL.
C
      COMMON/TEMGRD/NTGRAD
      COMMON/ZGRAD/NTSGD
      FUNCT1(T1,T2,T3,ZETA) = T1 + T2*ZETA + T3*ZETA**2
      FUNCT2(T1,T2,T3,ZETA) = T1 + T2*ZETA**T3
      FUNCT3(T1,T2,T3,ZETA) = T1 + T2*EXP(ZETA*T3)
      GO TO (10,20,30),NTGRAD
10    FUNCT = FUNCT1(T1,T2,T3,ZETA)
      GO TO 40
20    FUNCT = FUNCT2(T1,T2,T3,ZETA)
      GO TO 40
30    FUNCT = FUNCT3(T1,T2,T3,ZETA)
40    IF (NTSGD.EQ.0) RETURN
      IF (NTSGD.EQ.1) FUNCT = FUNCT*ZETA
      IF (NTSGD.EQ.2) FUNCT = FUNCT*ZETA*ZETA
      RETURN
      END
C=DECK      CFB2
      SUBROUTINE CFB2(THICK,II,K,RAD,NWALL,ZREF,I5,S,THERM,T1,T2,T3
     1,J,TIO,C,RADD,CUR1,CUR2)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C     SAME TRACE AS CFB1--WALLCF,READIT,MAIN.
C     CALCULATES CIJ FOR MONOCOQUE SHELL WITH VARIABLE THICKNESS.
C     THERMAL STRESS RESULTANTS NT AND MT ARE CALCULATED
C     MATERIAL PROPERTIES ARE ASSUMED TO BE TEMPERATURE-INDEPENDENT
C
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/SHMASS/SMPA
      COMMON/ZGRAD/NTSGD
      COMMON/WALPRP/EA(95),EB(95),GAB(95),UAB(95),ALPH1(95),ALPH2(95)
      DIMENSION TIO(I5,2),C(I5,14),RADD(100),CUR1(100),CUR2(100)
      DIMENSION THICK(I5),ZREF(I5),RAD(100),S(100),THERM(I5,4)
      COMMON/SHEL/ISHL(95),IWAL(95),ITHK(95),IARC(95),ILOAD(95)
      COMMON/OUTP/NPRT
      COMMON/AXIALZ/ZAX(100)
      COMMON/SFLAG/MONOQ
      COMMON/PRMFIL/IFILE,IFILE2,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
      COMMON/INDAT/INFILE
      CHARACTER*4 ANSOUT,CHARAC
      LOGICAL ANSL1
10    FORMAT(10I6)
20    FORMAT (6E12.8) 
30    FORMAT (/23H MODULUS OF ELASTICITY=E12.5,3X,14HPOISSON RATIO=E12.5
     1,3X,16HSHELL DENSITY  =E12.5,3X,20HTHERMAL EXP COEF.=   E12.5//)
40    FORMAT(/40H ANALYSIS IS FOR A MONOCOQUE SHELL             )
      IF (J.GT.1) GO TO 170
      IF (K.NE.1) GO TO 140
      WRITE (IFILE4,40)
C
C     E = MODULUS
C     U = POISSON,S RATIO
C     SM = WALL MATERIAL MASS DENSITY
C     ALPHA = THERMAL EXPANSION COEFFICIENT
C     ANRS = 1 SMEARED STRINGERS AND RINGS TO BE ADDED
C     ANRS = 0 NO SMEARED STRINGERS AND RINGS
C     SUR = 0 REFERENCE SURFACE IS MIDDLE SURFACE
C     SUR = 1 REFERENCE SURFACE IS OUTER SURFACE
C     SUR =-1 REFERENCE SURFACE IS LOCATED AN ARBITRARY DISTANCE FROM
C             THE WALL INNER SURFACE
C
C
      CALL DATUM(IFILE2,370,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,371,1,1,INT,E,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,372,1,1,INT,U,CHARAC,IOUT,0,1)
      IF (U.GT.1) THEN
         WRITE(IFILE4,*)' BAD POISSON RATIO, nu = ', U
         CALL ERREX
      ENDIF
      CALL DATUM(IFILE2,373,1,1,INT,SM,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,374,1,1,INT,ALPHA,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,375,1,2,INT,ANRS,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,376,1,2,INT,SUR,CHARAC,IOUT,0,1)
C
      EA(II) = E
      EB(II) = E
      UAB(II)= U
      GAB(II)= .5*E/(1.+U)
      ALPH1(II) = ALPHA
      ALPH2(II) = ALPHA
C
      NSUR = SUR
      IF (NSUR.EQ.0) GO TO 50
      IF (NSUR-1) 90,70,90
50    DO 60 KK=1,I5
60    THICK(KK) = 2.*ZREF(KK)
      GO TO 100
70    DO 80 KK=1,I5
80    THICK(KK) = ZREF(KK)
      GO TO 100
   90 CONTINUE
      CALL DATUM(IFILE2,377,1,2,NTYPET,REALL,CHARAC,IOUT,0,1)
      IF (NTYPET.NE.1.AND.NTYPET.NE.3) THEN
         WRITE(6,*)' NTYPET SHOULD BE EITHER  1  or  3 '
         CALL ERREX
      ENDIF
      IPRM(1) = 378
      IPRM(2) = 379
      CALL GETZ(NTYPET,I5,S,THICK,RAD,RADD,ZAX)
100   CONTINUE
      CALL GASP(THICK,I5,1,ITHK(II))
      WRITE (IFILE4,30)E,U,SM,ALPHA
      IF (NSUR.EQ.1) GO TO 140
      REWIND IFILE3
      CALL DATUM(IFILE3,606,1,1,INT,REALL,ANSOUT,IOUT,0,1)
      IF (ANSL1('N',ANSOUT,INFILE)) GO TO 140
      WRITE (IFILE4,110)
110   FORMAT(//49H MESH POINT   STATION   REF. SURFACE   THICKNESS   //)
      DO 120 KK=1,I5
120   WRITE (IFILE4,130)KK,S(KK),ZREF(KK),THICK(KK)
130   FORMAT(I8,1P3E14.5)
140   CONTINUE
      Z = ZREF(K)
      T = THICK(K  )
150   FORMAT(14H STATION S =    E17.8,8H  Z =    E17.8,8H  T =    E17.8)
160   CONTINUE
      E11 = E/(1.-U*U)
      E22 = E11
      E12 = U*E11
      TZ = T-Z
      TTZ = T**2/3. - T*Z + Z**2
      FMUL = 0.0
      FK1 = FMUL*CUR1(K)
      FK2 = FMUL*CUR2(K)
C
C     INTEGRATION THRU THICKNESS FOR MERIDIONAL FACES OF ELEMENT
C
      FK1S = FK1
      IF (-FK1*Z.LE.-1.0) FK1 = .9/Z
      IF( FK1*TZ.LE.-1.0) FK1 = -.9/TZ
      CALL INGRAT(-Z,TZ ,FK1,FK2,DZ,ZDZ,Z2DZ)
      FK1 = FK1S
      C11 = E11*DZ
      C12 = E12*T
      C14 = -E11*ZDZ
      C15 = -E12*(TZ**2 -Z**2)/2.
      C24 = C15
      C44 = E11*Z2DZ
      C45 = E12*(TZ**3+Z**3)/3.
C
C     INTEGRATION FOR CIRCUMFERENTIAL FACES OF ELEMENT
C
      FK2S = FK2
      IF (-FK2*Z.LE.-1.0) FK2 = .9/Z
      IF( FK2*TZ.LE.-1.0) FK2 = -.9/TZ
      CALL INGRAT(-Z,TZ,FK2,FK1,DZ,ZDZ,Z2DZ)
      FK2 = FK2S
      C22 = E22*DZ
      C25 = - E22*ZDZ
      C55 = E22*Z2DZ
      C33 = E*T/(2.*(1.+U))
      C36 = C33*(0.5*T-Z)
      C66 = C33*TTZ
      SMPA = SM*T
C     THERMAL STRESS RESULTANTS CALCULATED FOR KTH POINT IN IITH SEGMENT
  170 CONTINUE
      NUM = 25
      Z1 = - ZREF(K)
      Z2 = - ZREF(K) + THICK(K)
      NTSGD = 0
C
C     INNER AND OUTER SURFACE TEMPERATURE RISES ABOVE AMBIENT
      TIO(K,1) = FUNCT(T1,T2,T3,Z1)
      TIO(K,2) = FUNCT(T1,T2,T3,Z2)
      CALL FINDTH(Z1,Z2,T1,T2,T3,NUM,TN1)
      NTSGD = 1
      CALL FINDTH(Z1,Z2,T1,T2,T3,NUM,TM1)
C
C     THERMAL STRESS RESULTANTS AND THERMAL MOMENT RESULTANTS
      THERM(K,1) = - TN1*ALPHA*E/(1.-U)
      THERM(K,2) = THERM(K,1)
      THERM(K,3) = TM1*ALPHA*E/(1.-U)
      THERM(K,4) = THERM(K,3)
      THERM(K,1) = THERM(K,1)  -FK2*THERM(K,3)
      THERM(K,2) = THERM(K,2)  -FK1*THERM(K,4)
      NTSGD  = 2
      CALL FINDTH(Z1,Z2,T1,T2,T3,NUM,TL1)
      THERM(K,3) = (TM1 + TL1*FK2)*ALPHA*E/(1.-U)
      THERM(K,4) = (TM1 + TL1*FK1)*ALPHA*E/(1.-U)
      IF (J.GT.1) GO TO 190
180   NRS = ANRS
      IF (NRS.EQ.0) RETURN
C
C     CFB1 CALLED IF MONOCOQUE SHELL SEGMENT IS MERIDIONALLY AND/OR
C     CIRCUMFERENTIALLY STIFFENED BY STIFFENERS WHICH CAN BE SMEARED OUT
C     FOR THE PURPOSE  OF ANALYSIS.
      CALL CFB1(K,RAD,NWALL,Z,T,I5,S,C,RADD)
190   RETURN
      END
C=DECK      CFB3
      SUBROUTINE CFB3(I)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C        SUBROUTINE FOR SKEW STIFFENED SHELLS.
C     THIS SUBROUTINE HAS NOT BEEN CAREFULLY CHECKED OUT.
C
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/SRBLOK/TD,Z,ZN,EXA,EXB
      COMMON/SHMASS/SMPA
10    FORMAT(6E12.8)
20    FORMAT (/23H MODULUS OF ELASTICITY=E15.8,5X,14HPOISSON RATIO=E15.8
     1,5X,16HSHELL DENSITY  =E15.8//17H STIFFENER ANGLE= E15.8/
     219H STIFFENER SPACING=E15.8,5X,20HSTIFFENER THICKNESS=E15.8,5X,
     317HSTIFFENER HEIGHT=E15.8)
30    FORMAT (/25H STIFFENERS ARE INTERNAL.)
40    FORMAT (/25H STIFFENERS ARE EXTERNAL.) 
50    FORMAT (/40H ANALYSIS IS FOR A SKEW STIFFENED SHELL.) 
C
C
      IF (I.NE.1) GO TO 110
      WRITE (6,50)
      READ (5,10)E,U,T,SM
      READ (5,10)TH,A,B,H,AK,STFMD
C
C     E=MODULUS OF ELASTICITY OF SKIN
C     U=POISSON'S RATIO OF SKIN
C     T=THICKNESS OF SKIN
C     THS=ANGLE BETWEEN STIFFENERS AND SHELL GENERATOR (DEGREES)
C     A=STIFFENER SPACING ALONG CIRCUMFERENCE
C     B=STIFFENER THICKNESS (NORMAL)
C     H=STIFFENER HEIGHT TO NEAREST WALL SURFACE
C     AK=INDICATOR FOR STIFFENER LOCATION
C            IF 0 ... STIFFENERS INSIDE
C            IF 1 ... STIFFENERS OUTSIDE
C
      WRITE (6,20)E,U,SM,TH,A,B,H
      IF (AK) 60,60,70
60    WRITE (6,30)
      GO TO 80
70    WRITE (6,40)
80    TH=3.1415927*TH/180.
      U2=1.-U*U
      GA=T**2/12.
      UM=2.*B*H*U2/(A*T)
      ETT=B*H**3*U2/(6.*A*T)
      ET=H*B**3*(1.-U)/(1.5*A*T)
      RH=0.5*(H+T)
      IF (AK) 90,90,100
90    RH=-RH
100   S=SIN(TH)
      C=COS(TH)
      ZL=S/C
      S1=C*S**2
      S3=S**3
      S4=C**3
      RM=2.*UM*S1/(1.-U)
      RH1=RH/(1.+RM)
      RH2=RH-RH1
      Z=-RH2+T/2.
      C11=1.+UM*S4
      C12=U+UM*S1
      C14=RH2-UM*RH1*S4
      C15=U*RH2-UM*RH1*S1
      C22=1.+ZL*UM*S3
      C24=C15
      C25=RH2-ZL*UM*RH1*S3
      C33=0.5*(1.-U)+UM*S1
      C44=GA+RH2**2+(UM*RH1**2+ETT)*S4+ET*S1
      C45=U*(GA+RH2**2)+(UM*RH1**2+ETT)*S1-ET*S1
      C55=GA+RH2**2+ZL*(UM*RH1**2+ETT)*S3+ET*S1
      C66=2.*(1.-U)*GA+2.*(1.-U)*RH2**2+4.*(UM*RH1**2+ETT)*S1+
     1ET*(C**2-S**2)**2/C
      FNORM=E*T/(1.0-U*U)
      C11=C11*FNORM
      C12=C12*FNORM
      C14=C14*FNORM
      C15=C15*FNORM
      C22=C22*FNORM
      C24=C24*FNORM
      C25=C25*FNORM
      C33=C33*FNORM
      C44=C44*FNORM
      C45=C45*FNORM
      C55=C55*FNORM
      C66=C66*FNORM
      SMPA = SM*T + STFMD*B*H/(A*C)
110   RETURN
      END
C=DECK      CFB4
      SUBROUTINE CFB4(ISEG,IPOINT,RAD,NWALL,Z,I5,S,C,RADD,THERM,
     1 T1,T2,T3,TIO)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C  OBTAIN CONSTITUTIVE LAW FOR COMPOSITE WALL WITH LAMINAE AT
C  VARIOUS ANGLES WITH RESPECT TO THE MERIDIONAL COORDINATE.
C
C
C BEG OCT 2004
      COMMON/ZGRAD/NTSGD
C END OCT 2004
      COMMON/LAYER/MATL(90),LTYPE(1500),T(90),ANGLE(90),NLAYER(95)
      COMMON/MATER1/E1(20),E2(20),G(20),FNU(20),DENS(20)
      COMMON/MATERT/ALPHA1(20),ALPHA2(20),CURETP(20),EALLOW(5,20)
      COMMON/JWATCH/JOLD
      COMMON/CMAXS/CMAX
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/SHMASS/SMPA
      COMMON/COMNOD/NODES(100),NODALL(100),NODTOT
C
      DIMENSION CX(6,6),THERMX(6),CNEW(6,6),RAD(*),RADD(*),S(*)
      DIMENSION E1L(99),E2L(99),GL(99),ZETL(99),U12L(99),RHOL(99)
      DIMENSION A1L(99),A2L(99),A1L1(99),A1L2(99),A2L1(99),A2L2(99)
      DIMENSION TL(99),C(I5,14),THERM(I5,4),THMSFT(6),TIO(I5,2)
C
      IF (ISEG.EQ.1.AND.IPOINT.EQ.1) THEN
         JOLD = 0
         CALL MOVER(0,0,MATL,1,90)
      ENDIF
      IF (IPOINT.NE.1) GO TO 100
      CALL INPROPW(NLAY,LTYPE,T,ANGLE,MATL,E1,E2,G,FNU,
     1           ALPHA1,ALPHA2,DENS,NRS,CURETP,EALLOW,JOLD)
C
      CALL NODOUT(NWALL,ISEG,I5,NODES,NODALL,NODTOT)
C
      TD = 0.
      DO 30 J = 1,NLAY
C
      JJ = J + JOLD
      K = LTYPE(JJ)
      M = MATL(K)
C
      E1L(J) = E1(M)
      E2L(J) = E2(M)
       GL(J) =  G(M)
      U12L(J)= FNU(M)
      RHOL(J)= DENS(M)
       TL(J) =  T(K)
       TD    = TD + TL(J)
      ZETL(J)= ANGLE(K)
      A1L(J) = ALPHA1(M)*(-CURETP(M))
      A2L(J) = ALPHA2(M)*(-CURETP(M))
      A1L1(J)= A1L(J)
      A1L2(J)= A1L(J)
      A2L1(J)= A2L(J)
      A2L2(J)= A2L(J)
   30 CONTINUE
C
      JOLD = JOLD + NLAY
      NLAYER(ISEG) = NLAY
  100 CONTINUE
      Z1 = -Z
      Z2 = Z1 + TD
C BEG OCT 2004
      NTSGD = 0
C END OCT 2004
      TIO(IPOINT,1) = FUNCT(T1,T2,T3,Z1)
      TIO(IPOINT,2) = FUNCT(T1,T2,T3,Z2)
      BI = 0.5*TL(1)
      TEMP1 = TIO(IPOINT,1)
      DT = TIO(IPOINT,2) - TEMP1
      DO 120 J = 1,NLAY
         TEMP = TEMP1 + DT*BI/TD
         TEMPL= TEMP1 + DT*(BI-0.5*TL(J))/TD
         TEMPR= TEMP1 + DT*(BI+0.5*TL(J))/TD
         JJ = J + JOLD - NLAY
         K = LTYPE(JJ)
         M = MATL(K)
         A1L(J) = ALPHA1(M)*(-CURETP(M)+TEMP)
         A2L(J) = ALPHA2(M)*(-CURETP(M)+TEMP)
         A1L1(J)= ALPHA1(M)*(-CURETP(M)+TEMPL)
         A2L1(J)= ALPHA2(M)*(-CURETP(M)+TEMPL)
         A1L2(J)= ALPHA1(M)*(-CURETP(M)+TEMPR)
         A2L2(J)= ALPHA2(M)*(-CURETP(M)+TEMPR)
         IF (J.LT.NLAY) BI = BI + 0.5*(TL(J)+TL(J+1))
  120 CONTINUE
      ZREF = 0.
      CALL CFBL(NLAY,ZREF,E1L,E2L,GL,U12L,RHOL,TL,ZETL,CX,
     1     A1L,A2L,THERMX,TMS,TD,A1L1,A1L2,A2L1,A2L2)
      DZREF = Z - TD/2.
      CALL CSHIFT(CX,DZREF,CNEW,THERMX,THMSFT)
      SMPA = TMS
C
      C11 = CNEW(1,1)
      C12 = CNEW(1,2)
      C13 = 0.
      C14 = CNEW(1,4)
      C15 = CNEW(1,5)
      C16 = 0.
      C22 = CNEW(2,2)
      C23 = 0.
      C24 = CNEW(2,4)
      C25 = CNEW(2,5)
      C26 = 0.
      C33 = CNEW(3,3)
      C34 = 0.
      C35 = 0.
      C36 = CNEW(3,6)
      C44 = CNEW(4,4)
      C45 = CNEW(4,5)
      C46 = 0.
      C55 = CNEW(5,5)
      C56 = 0.
      C66 = CNEW(6,6)
C
      THERM(IPOINT,1) = -THERMX(1)
      THERM(IPOINT,2) = -THERMX(2)
      THERM(IPOINT,3) =  THMSFT(4)
      THERM(IPOINT,4) =  THMSFT(5)
C
      IF (NRS.NE.0) CALL CFB1(IPOINT,RAD,NWALL,Z,TD,I5,S,C,RADD)
C      
      RETURN
      END
C
C
C
C=DECK      INPROPW
      SUBROUTINE INPROPW(NLAY,LTYPE,T,ANGLE,MATL,E1,E2,G,FNU,
     1            ALPHA1,ALPHA2,DENS,NRS,CURETP,EALLOW,JOLD)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C  PURPOSE IS TO READ IN PROPERTIES OF LAYERED WALL WITH LAMINAE
C  AT VARIOUS WINDING ANGLES.
C
      DIMENSION LTYPE(*),T(*),ANGLE(*),MATL(*),E1(*),E2(*),G(*)
      DIMENSION FNU(*),ALPHA1(*),ALPHA2(*),DENS(*),CURETP(*)
      DIMENSION EALLOW(5,20)
C BEG APR 1992
      DIMENSION IMATL(90)
C END APR 1992
C
      COMMON/PRMFIL/IFILEX,IFILE,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
      COMMON/INDAT/INFILE
      CHARACTER*4 ANSOUT,CHARAC
      LOGICAL ANSL1
C
      IPROMP = 1
      REWIND IFILE
      CALL DATUM(IFILE,384,0,0,INT,REALL,CHARAC,IOUT,0,IPROMP)
      CALL DATUM(IFILE,385,1,2,NLAY,REALL,CHARAC,IOUT,0,IPROMP)
C
C BEG APR 1992
      DO 10 J = 1,NLAY
      IMATL(J) = 0
   10 CONTINUE
      MAXMAT = 0
      LKOUNT = 0
C END APR 1992
      DO 30 J = 1,NLAY
      L = J + JOLD
      REWIND IFILE
   20 CONTINUE
      CALL DATUM(IFILE,386,1,2,LTYPE(L),REALL,CHARAC,IOUT,J,IPROMP)
      CALL DATUM(IFILE,387,1,1,INT,REALL,ANSOUT,IOUT,0,IPROMP)
      IF (ANSL1('N',ANSOUT,INFILE)) GO TO 30
C
C  NEW LAYER TYPE.  READ IN THICKNESS, WINDING ANGLE, MATERIAL TYPE...
C
      K = LTYPE(L)
      CALL DATUM(IFILE,388,0,0,INT,REALL,CHARAC,IOUT,K,IPROMP)
      CALL DATUM(IFILE,389,1,1,INT,T(K),CHARAC,IOUT,K,IPROMP)
      CALL DATUM(IFILE,390,1,2,INT,ANGLE(K),CHARAC,IOUT,K,IPROMP)
      CALL DATUM(IFILE,391,1,1,MATL(K),REALL,CHARAC,IOUT,K,IPROMP)
C BEG APR 1992
      LKOUNT = LKOUNT + 1
      IF (LKOUNT.EQ.1) THEN
         IMATL(J) = MATL(K)
         MAXMAT = 1
C BEG JAN 1993 (FOLLOWING STATEMENT IS COMMENTED OUT)
C        GO TO 26
C END JAN 1993
      ENDIF
      IF (J.GT.1) THEN
         L1 = J - 1
         DO 25 LL = 1,L1
            IF (MATL(K).EQ.IMATL(LL)) GO TO 26
   25    CONTINUE
         IMATL(J) = MATL(K)
   26    CONTINUE
      ENDIF
   30 CONTINUE
      IF (MAXMAT.GT.0)
     1 CALL DATUM(IFILE,392,0,0,INT,REALL,CHARAC,IOUT,0,IPROMP)
      DO 200 J = 1,NLAY
      I = IMATL(J)
      IF (I.EQ.0) GO TO 200
C END APR 1992
C
      REWIND IFILE
      CALL DATUM(IFILE,393,1,2,INT,REALL,ANSOUT,IOUT,I,IPROMP)
      IF (ANSL1('N',ANSOUT,INFILE)) GO TO 200
      WRITE(6,115) I
  115 FORMAT(//' MATERIAL PROPERTIES FOR MATERIAL TYPE',I3)
C
      CALL DATUM(IFILE,394,0,0,INT,REALL,CHARAC,IOUT,0,IPROMP)
      CALL DATUM(IFILE,395,1,1,INT,E1(I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,396,1,1,INT,E2(I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,397,1,1,INT, G(I),CHARAC,IOUT,I,IPROMP)
      KKOUNT = 0
  150 CONTINUE
      CALL DATUM(IFILE,398,1,1,INT,FNU(I),CHARAC,IOUT,I,IPROMP)
      FNUTST = 1000000.
      IF (E2(I).NE.0.) FNUTST = FNU(I)*E1(I)/E2(I)
      DIF    = 1. - FNUTST*FNU(I)
      IF (DIF.LE.0.) THEN
         WRITE(6,155)
  155    FORMAT(/' BAD VALUE FOR POISSONS RATIO. NOTE THAT THERE ARE'/
     1 ' TWO POISSONS RATIOS, NU12 AND NU21. THE ONE THAT YOU ARE'/
     1 ' ASKED TO PROVIDE, NU12, IS GIVEN BY:'/
     1 '               NU12  = NU21*E2/E1'/
     1 ' THEREFORE, IF E1 IS LARGER THAN E2 (WHICH IS USUALLY THE'/
     1 ' CASE), YOU MUST PROVIDE AS INPUT HERE THE SMALLER OF THE TWO'/
     1 ' POISSONS RATIOS.'/)
C
         BACKSPACE(IOUT)
         REWIND IFILE
         GO TO 150
      ENDIF
C
      IF (FNUTST.GT.1.0.AND.KKOUNT.EQ.0.AND.E2(I).NE.0.) THEN
         WRITE(6,156)
  156    FORMAT(/' POISSONS RATIO MAY BE TOO BIG.  NOTE THAT THERE ARE'/
     1 ' TWO POISSONS RATIOS, NU12 AND NU21. THE ONE THAT YOU ARE'/
     1 ' ASKED TO PROVIDE, NU12, IS GIVEN BY:'/
     1 '               NU12  = NU21*E2/E1'/
     1 ' THEREFORE, IF E1 IS LARGER THAN E2 (WHICH IS USUALLY THE'/
     1 ' CASE), YOU MUST PROVIDE AS INPUT HERE THE SMALLER OF THE TWO'/
     1 ' POISSONS RATIOS.'/)
         WRITE(6,157)
  157    FORMAT(/' PANDA2 WILL ASK YOU AGAIN FOR THE POISSONS RATIO.'/
     1 ' PLEASE MAKE SURE THAT YOU SUPPLY THE SMALLER OF THE TWO'/
     1 ' POISSONS RATIOS FOR THIS ORTHOTROPIC MATERIAL.'/)
         BACKSPACE(IOUT)
         REWIND IFILE
         KKOUNT = KKOUNT + 1
         GO TO 150
      ENDIF
C
  160 CONTINUE
      CALL DATUM(IFILE,399,1,1,INT,ALPHA1(I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,400,1,1,INT,ALPHA2(I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,401,1,2,INT,CURETP(I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,402,1,2,INT,DENS(I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,403,0,0,INT,REALL,CHARAC,IOUT,0,IPROMP)
      CALL DATUM(IFILE,404,1,1,INT,EALLOW(1,I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,405,1,1,INT,EALLOW(2,I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,406,1,1,INT,EALLOW(3,I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,407,1,1,INT,EALLOW(4,I),CHARAC,IOUT,I,IPROMP)
      CALL DATUM(IFILE,408,1,1,INT,EALLOW(5,I),CHARAC,IOUT,I,IPROMP)
  200 CONTINUE
      CALL DATUM(IFILE,409,1,2,NRS,REALL,CHARAC,IOUT,0,IPROMP)
C
      RETURN
      END
C
C
C
C=DECK      CFBL
      SUBROUTINE CFBL(NLAY,ZREF,E1L,E2L,GL,U12L,RHOL,TL,ZETL,CCC,
     1                A1L,A2L,THERM,TMS,TD,A1L1,A1L2,A2L1,A2L2)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C  PURPOSE IS TO CALCULATE C(i,j) AND THERM(J) FOR ONE MULTI-LAYERED
C  SKIN.
C
      DIMENSION E1L(99),E2L(99),GL(99),ZETL(99),U12L(99),RHOL(99)
      DIMENSION A1L(99),A2L(99),THL(2),THERM(6)
      DIMENSION A1L1(99),A1L2(99),A2L1(99),A2L2(99),THLDIF(2)
      DIMENSION S(99),R(3,3),TL(99),CCC(6,6)  
      DIMENSION CL(3,3),CP(3,3),CT(3,3),CTP(3,3)
      DATA DTR,RTD,PI /.017453293, 57.29577951, 3.141592654/
C
      CALL MOVER(0.,0,CCC,1,36)
      CALL MOVER(0.,0,THERM,1,6)
      B = 1.
      A = 1.
      C = 0.
C
      TD = 0.     
      TMS = 0.    
      DO 10 L=1,NLAY    
      U21 = 0.
      IF (E2L(L).GT.0.) U21 = U12L(L)*E1L(L)/E2L(L)   
      UD = 1.-U12L(L)*U21     
      S(L) = E1L(L)/UD  
      TD = TD+TL(L)     
   10 TMS = TMS+TL(L)*RHOL(L) 
C
      ECCREF = (0.5*TD - ZREF)
      IF (ZREF.EQ.0.) ECCREF = 0.
      BI = .5*(TD-TL(1))
      KT = 1
C     ------------------------------------------------------------------
C       INTEGRATION THROUGH TOTAL THICKNESS     
C     ------------------------------------------------------------------
      DO 100 L=1,NLAY   
      CALL MOVER (0,0,CL,1,9) 
      CALL MOVER (0,0,CT,1,9)
      CALL MOVER (0,0,R,1,9)  
      H = - ECCREF - BI   
      IF (E1L(L).EQ.0.) GO TO 77    
C     ------------------------------------------------------------------
C       LAYER CONSTITUTIVE MATRIX (W.R.T. PHI1(L),PHI2(L))  
C     ------------------------------------------------------------------
      CL(1,1) = S(L)    
      CL(1,2) = U12L(L)*S(L)  
      CL(2,1) = CL(1,2) 
      CL(2,2) = 0.
      IF (E1L(L).GT.0.) CL(2,2) = S(L)*E2L(L)/E1L(L)  
      CL(3,3) = GL(L)   
C
C     COMPUTE STRAIN TRANSFORMATION MATRIX FROM ELEMENT TO FIBER AXES   
C
      ZA = ZETL(L)*DTR  
      CALL RTRSF (A,B,C,ZA,R) 
      CALL CONTRA (R,3,3,CL,3,CP)   
      CALL CONTRA (R,3,3,CT,3,CTP)
C
C  THERMAL RESULTANTS, NT, AND MOMENTS, MT...
C
      ET1 = CL(1,1)*A1L(L)+CL(1,2)*A2L(L)
      ET2 = CL(2,1)*A1L(L)+CL(2,2)*A2L(L)
      THL(1) = R(1,1)*ET1+R(2,1)*ET2
      THL(2) = R(1,2)*ET1+R(2,2)*ET2
      ET11= CL(1,1)*A1L1(L)+CL(1,2)*A2L1(L)
      ET12= CL(1,1)*A1L2(L)+CL(1,2)*A2L2(L)
      ET21= CL(2,1)*A1L1(L)+CL(2,2)*A2L1(L)
      ET22= CL(2,1)*A1L2(L)+CL(2,2)*A2L2(L)
      THLDIF(1) = R(1,1)*(ET12-ET11) +R(2,1)*(ET22-ET21)
      THLDIF(2) = R(1,2)*(ET12-ET11) +R(2,2)*(ET22-ET21)
   72 DZ = TL(L)  
      ZDZ = H*DZ  
      Z2DZ = DZ*(H**2+DZ**2/12.)    
C     ------------------------------------------------------------------
C       ACCUMULATE CONSTITUTIVE MATRIX (W.R.T. XP,YP) 
C     ------------------------------------------------------------------
      DO 1000 I=1,3     
      DO 1000 J=1,3     
      CCC(I,J) = CCC(I,J)+CP(I,J)*DZ
      CCC(I,J+3) = CCC(I,J+3)-CP(I,J)*ZDZ 
      CCC(I+3,J+3) = CCC(I+3,J+3)+CP(I,J)*Z2DZ  
 1000 CONTINUE    
C
C  THERMAL TERMS...
C
      DO 1100 J = 1,2
         THERM(J) = THERM(J) + THL(J)*DZ
         THERM(J+3) = THERM(J+3) - THL(J)*ZDZ -THLDIF(J)*TL(L)**2/12.
 1100 CONTINUE
C
   77 IF (L.EQ.NLAY) GO TO 100
      BI = BI-.5*(TL(L)+TL(L+1))    
  100 CONTINUE    
C
C  CHANGE SIGN OF C(3,6) TO AGREE WITH BOSOR4 CONVENTION...
C
      CCC(3,6) = - CCC(3,6)
      CCC(6,3) =   CCC(3,6)
C
C  COMPLETE TRANSVERSE SHEAR STIFFNESS CALCULATION...
C
C  SYMMETRIZE CCC...
C
      DO 1200 I = 1,6
      DO 1200 J = I,6
 1200 CCC(J,I) = CCC(I,J)
C
      RETURN
      END   
C
C
C
C=DECK      MOVER 
c     SUBROUTINE MOVER (KK,II,LL,JJ,N)    
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C        MOVE 4-BYTE WORDS FROM KK TO LL  
C     
c     DIMENSION KK(*), LL(*)  
c     IF(N.LT.1) RETURN 
c     I=1   
c     J=1   
c     DO 10 K=1,N 
c     LL(J)=KK(I) 
c     I=I+II
c  10 J=J+JJ
c     RETURN
c     END   
C
C
C
C=DECK      CONTRA
      SUBROUTINE CONTRA (T,MT,NT,A,MA,C)  
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
      DIMENSION T(MT,NT),A(MA,MA),C(NT,NT)
      IF(MT.NE.MA) GO TO 99   
      MC =NT*NT    
      CALL MOVER (0,0,C,1,MC) 
      DO 1 K=1,MA 
      DO 1 L=1,MA 
      V=A(K,L)    
      IF (V.EQ.0.) GO TO 1    
      DO 2 I=1,NT 
      DO 2 J=1,NT 
    2 C(I,J)=C(I,J)+T(K,I)*V*T(L,J) 
    1 CONTINUE    
      RETURN
   99 WRITE (6,111)     
  111 FORMAT (63H * DIMENSIONS OF T AND A ARE INCONSISTENT IN SUBROUTINE
     1 CONTRA*)   
      CALL EXIT   
      END   
C
C
C
C=DECK      RTRSF 
      SUBROUTINE RTRSF(A,B,C,Z,R)   
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
      DIMENSION R(3,3)  
C    TRANSFORMATION MATRIX FOR STRAINS FROM NONORTHOGONAL TO ORTHOGONAL 
C          WITH ANGLE Z 
      D=A*A*B*B-C*C     
      IF(D.LE.0) GO TO 100    
      D=SQRT(D)   
      CS=C/D
      S=A*B/D     
      CZ=COS(Z)   
      SZ=SIN(Z)   
      B11=CZ-CS*SZ
      B12=-(CS*CZ+SZ)   
      B21=S*SZ    
      B22=S*CZ    
      R(1,1)=B11*B11    
      R(1,2)=B21*B21    
      R(1,3)=B11*B21    
      R(2,1)=B12*B12    
      R(2,2)=B22*B22    
      R(2,3)=B12*B22    
      R(3,1)=2.*B11*B12 
      R(3,2)=2.*B21*B22 
      R(3,3)=B11*B22+B21*B12  
      GO TO 101   
  100 CONTINUE    
      WRITE (6,200)     
  200 FORMAT (//,66H ** IMPROPER SHELL GEOMETRY DETECTED BY RTRSF..A**2*
     1B**2-C**2=0 **)   
      CALL EXIT   
  101 CONTINUE    
      RETURN
      END   
C
C
C
C=DECK      NEUTAX
      SUBROUTINE NEUTAX(C,NX,NY,DNEUT)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C  Purpose is to find the neutral planes of the panel with smeared
C  stiffeners.
C
C  Input:  C = 6x6 constitutive law for panel with smeared stiffeners
C         NX = axial load/circ. arc (Nx = 1.0 for calc. of DNEUTX, 
C                                         0.0 for calc. of DNEUTY)
C         NY = circ. load/axial arc (Ny = 0.0 for calc. of DNEUTX,
C                                         1.0 for calc. of DNEUTY)
C
C  Output:
C      DNEUT = distance from middle surface of skin to neutral plane
C
      DIMENSION C(6,6)
      REAL NX, NY
C
      B11 = C(2,2)*C(1,4) - C(1,2)*C(2,4)
      B12 = C(2,2)*C(1,5) - C(1,2)*C(2,5)
      B21 = C(1,1)*C(2,4) - C(1,2)*C(1,4)
      B22 = C(1,1)*C(2,5) - C(1,2)*C(1,5)
C
      DEN1= C(1,1)*C(2,2) - C(1,2)**2
      C44BAR = C(4,4) -(B11*C(1,4) + B21*C(2,4))/DEN1
      C45BAR = C(4,5) -(B12*C(1,4) + B22*C(2,4))/DEN1
      C54BAR = C(4,5) -(B11*C(1,5) + B21*C(2,5))/DEN1
      C55BAR = C(5,5) -(B12*C(1,5) + B22*C(2,5))/DEN1
C
      Q1 = - (C(1,4)*(C(2,2)*NX - C(1,2)*NY)
     1     +  C(2,4)*(C(1,1)*NY - C(1,2)*NX) )/DEN1
      Q2 = - (C(1,5)*(C(2,2)*NX - C(1,2)*NY)
     1     +  C(2,5)*(C(1,1)*NY - C(1,2)*NX) )/DEN1
C
      DEN = -C55BAR*C44BAR + C54BAR*C45BAR
      F1  = Q2*C45BAR - Q1*C55BAR
      F2  = Q2*C44BAR - Q1*C54BAR
C
      IF (NX.GT.0.) DNEUT = -( F1*(-C44BAR) - F2*(-C45BAR) )/(DEN*NX)
      IF (NY.GT.0.) DNEUT = -( F2*( C55BAR) - F1*( C54BAR) )/(DEN*NY)
C
      RETURN
      END
C
C
C
C=DECK      CSHIFT
      SUBROUTINE CSHIFT(C,D,CNEW,THMOLD,THMNEW)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C  PURPOSE IS TO DERIVE CNEW(I,J) FOR NEW REFERENCE SURFACE LOCATED A
C  DISTANCE D FROM THE OLD REFERENCE SURFACE.
C
C  INPUT...
C
C    C = OLD CONSTITUTIVE MATRIX
C    D = REFERENCE SURFACE SHIFT
C   THMOLD = OLD THERMAL RESULTANT
C   THMNEW = SHIFTED THERMAL RESULTANT
C
C  OUTPUT...
C
C    CNEW = NEW CONSTITUTIVE MATRIX
C
      DIMENSION C(6,6),CNEW(6,6),THMOLD(6),THMNEW(6)
C
      CALL MOVER(THMOLD,1,THMNEW,1,6)
C
      DO 10 I = 1,3
      DO 10 J = 1,3
         CNEW(I,J) = C(I,J)
   10 CONTINUE
C
      CNEW(1,4) = C(1,4) + D*C(1,1)
      CNEW(4,1) = CNEW(1,4)
      CNEW(1,5) = C(1,5) + D*C(1,2)
      CNEW(5,1) = CNEW(1,5)
      CNEW(1,6) = C(1,6) - D*C(1,3)
      CNEW(6,1) = CNEW(1,6)
C
      CNEW(2,4) = C(2,4) + D*C(1,2)
      CNEW(4,2) = CNEW(2,4)
      CNEW(2,5) = C(2,5) + D*C(2,2)
      CNEW(5,2) = CNEW(2,5)
      CNEW(2,6) = C(2,6) - D*C(2,3)
      CNEW(6,2) = CNEW(2,6)
C
      CNEW(3,4) = C(3,4) + D*C(1,3)
      CNEW(4,3) = CNEW(3,4)
      CNEW(3,5) = C(3,5) + D*C(2,3)
      CNEW(5,3) = CNEW(3,5)
      CNEW(3,6) = C(3,6) - D*C(3,3)
      CNEW(6,3) = CNEW(3,6)
C
      CNEW(4,4) = C(4,4) + 2.*D*C(1,4) + D*D*C(1,1)
      CNEW(4,5) = C(4,5) + D*(C(2,4)+C(1,5)) + D*D*C(1,2)
      CNEW(5,4) = CNEW(4,5)
      CNEW(4,6) = C(4,6) + D*(C(1,6)-C(3,4)) - D*D*C(1,3)
      CNEW(6,4) = CNEW(4,6)
C
      CNEW(5,5) = C(5,5) + 2.*D*C(2,5) + D*D*C(2,2)
      CNEW(5,6) = C(5,6) + D*(C(2,6)-C(3,5)) - D*D*C(2,3)
      CNEW(6,5) = CNEW(5,6)
C
      CNEW(6,6) = C(6,6) - 2.*D*C(3,6) + D*D*C(3,3)
C
      THMNEW(4) = -THMOLD(4) - D*THMOLD(1)
      THMNEW(5) = -THMOLD(5) - D*THMOLD(2)
C
      RETURN
      END
C
C
C
C=DECK      CFB5
      SUBROUTINE CFB5(K,RAD,NWALL,Z,I5,THERM,T1,T2,T3,J,S,C,RADD,
     1ISEG,TIO)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C     SUBROUTINE FOR ORTHOTROPIC LAYERED SHELLS
C     MATERIAL PROPERTIES ARE ASSUMED TO BE TEMPERATURE-INDEPENDENT
C     THERMAL STRESS RESULTANTS NT AND MT ARE CALCULATED
C
      DIMENSION RAD(100),RADD(100),ALPHA1(20),ALPHA2(20),THERM(I5,4)
      COMMON/ZGRAD/NTSGD
      DIMENSION TIO(I5,2)
      COMMON/SFLAG/ISTRES
      COMMON/STRSEG/ISTRSS(95)
      COMMON/WALPRP/EA(95),EB(95),GAB(95),UAB(95),ALPH1(95),ALPH2(95)
      COMMON/NLAMIN/LAMINA(95)
      COMMON/SHEL/ISHL(95),IWAL(95),ITHK(95),IARC(95),ILOAD(95)
      COMMON/AXIALZ/ZAXIAL(100)
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/SHMASS/SMPA
      COMMON/PRMFIL/IFILE,IFILE2,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
      COMMON/INDAT/INFILE
      CHARACTER*4 ANSOUT,CHARAC
      LOGICAL ANSL1
      DIMENSION SM(20),T(20),S(100),C(I5,14)
      DIMENSION ITIN(50),THICK(100,16),TIN(50)
      DIMENSION G(20),EX(20),EY(20),UXY(20),UYX(20),U1(20)
10    FORMAT(10I6) 
20    FORMAT (6E12.8) 
30    FORMAT(/59X,10HMODULUS OF,7X,7HPOISSON,8X,7HDENSITY/35X,5HLAYER,5X
     1,9HTHICKNESS,5X,10HELASTICITY,8X,5HRATIO)
40    FORMAT(37X,I2,2X,4E15.8) 
50    FORMAT(/28X,5HSHEAR,9X,6HYOUNGS,9X,6HYOUNGS,9X,7HPOISSON          
     1,8X, 7HDENSITY,7X,7HTHERMAL,9X,7HTHERMAL  /
     21X,5HLAYER,5X,9HTHICKNESS,7X,7HMODULUS,6X,10HMODULUS(X),5X,
     310HMODULUS(Y),6X,9HRATIO(XY),20X,11HEXP.COEF(X),5X,11HEXP.COEF(Y))
60    FORMAT(3X,I2,2X,8E15.8)
70    FORMAT(///63H  ANALYSIS IS FOR A LAYERED SHELL.   INNER LAYER IS N
     1UMBER 1   ) 
C
C
      IF (J.GT.1) GO TO 240
      IF (K.NE.1) GO TO 170
      WRITE (IFILE4,70)
      CALL DATUM(IFILE2,410,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,411,1,1,INT,WRAPS,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,412,1,2,INT,ANRS,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,413,1,2,INT,TYPET,CHARAC,IOUT,0,1)
C
C     WRAPS = NO. OF LAYERS
C     ANRS = 0 NO SMEARED STRINGERS OR RINGS
C     ANRS = 1    SMEARED STRINGERS OR RINGS
C     TYPET = 0 CONSTANT THICKNESS LAYERS
C     TYPET = 1 VARIABLE THICKNESS LAYERS,  READ LAYER THICKNESSES
C     TYPET = 2 VARIABLE THICKNESS LAYERS,  LAYER THICKNESS FROM FUNCT.
      LAYERS=WRAPS
      LAMINA(ISEG) = LAYERS
      IF (LAYERS.LT.1.OR.LAYERS.GT.20) THEN
        WRITE(6,*)' LAYERS IS NOT IN PERMISSIBLE RANGE 1 TO 20'
        CALL ERREX
      ENDIF
      DO 80 I=1,LAYERS
80    T(I) = 0.0
      NRS=ANRS
      NTYPET = TYPET
      IF (NTYPET.NE.0.AND.NTYPET.NE.1) THEN
        WRITE(6,*)' NTYPET NOT IN PERMISSIBLE RANGE 0 TO 1'
        CALL ERREX
      ENDIF
C
C     I = 1 . . INNER LAYER,   I = LAYERS . . OUTER LAYER
C     T =  THICKNESS
C     G =  SHEAR MODULUS
C     EX =  MERIDIONAL MODULUS
C     EY =  CIRCUMFERENTIAL MODULUS
C     UXY = POISSON,S RATIO . . EY*UXY = EX*UYX
C     SM = LAYER MATERIAL MASS DENSITY
C     ALPHA1 = MERIDIONAL THERMAL EXPANSION COEFFICIENT
C     ALPHA2 = CIRCUMFERENTIAL THERMAL EXPANSION COEFFICIENT
C
C
      IF (NTYPET.NE.0) GO TO 82
      DO 81 I = 1,LAYERS
      IPROMP = 1
      IF (I.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,414,1,1,INT,T(I),CHARAC,IOUT,I,IPROMP)
   81 CONTINUE
   82 CONTINUE
      DO 83 I = 1,LAYERS
      IPROMP = 1
      IF (I.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,415,1,1,INT,G(I),CHARAC,IOUT,I,IPROMP)
   83 CONTINUE
      DO 84 I = 1,LAYERS
      IPROMP = 1
      IF (I.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,416,1,1,INT,EX(I),CHARAC,IOUT,I,IPROMP)
   84 CONTINUE
      DO 85 I = 1,LAYERS
      IPROMP = 1
      IF (I.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,417,1,1,INT,EY(I),CHARAC,IOUT,I,IPROMP)
   85 CONTINUE
      DO 86 I = 1,LAYERS
      IPROMP = 1
      IF (I.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,418,1,1,INT,UXY(I),CHARAC,IOUT,I,IPROMP)
   86 CONTINUE
      DO 87 I = 1,LAYERS
      IPROMP = 1
      IF (I.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,419,1,1,INT,SM(I),CHARAC,IOUT,I,IPROMP)
   87 CONTINUE
      DO 88 I = 1,LAYERS
      IPROMP = 1
      IF (I.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,420,1,1,INT,ALPHA1(I),CHARAC,IOUT,I,IPROMP)
   88 CONTINUE
      DO 89 I = 1,LAYERS
      IPROMP = 1
      IF (I.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,421,1,1,INT,ALPHA2(I),CHARAC,IOUT,I,IPROMP)
   89 CONTINUE
C
      EA(ISEG) = EX(1)
      EB(ISEG) = EY(1)
      UAB(ISEG)= UXY(1)
      GAB(ISEG)= G(1)
      ALPH1(ISEG) = ALPHA1(1)
      ALPH2(ISEG) = ALPHA2(1)
C
      DO 90 I = 1,I5
   90 THICK(I,1) = T(1)
      IF (ISTRSS(ISEG).EQ.2) GO TO 100
      IF (LAYERS.GT.1) ISTRSS(ISEG) = 0
100   IF (NTYPET.EQ.0) GO TO 120
      IF (NTYPET.EQ.2) GO TO 115
C
C     NTIN = NO. OF MERIDIONAL STATIONS FOR WHICH LAYER THICKNESSES
C            TO BE READ IN.. LOCATION OF CALLOUTS DETERMINED IN STA
C     TIN = THICKNESS OF GIVEN LAYER AT THE NTIN CALLOUT POINTS DETER-
C           MINED IN SUBROUTINE STA.
C
      CALL DATUM(IFILE2,422,1,2,NTIN,REALL,CHARAC,IOUT,0,1)
      IF (NTIN.LT.2.OR.NTIN.GT.20) THEN
        WRITE(6,*)' NTIN NOT IN PERMISSIBLE RANGE 2 TO 20'
        CALL ERREX
      ENDIF
C
      IPRM(2) = 423
      CALL STA(ITIN,ZAXIAL,RAD,RADD,S,NTIN,I5)
      CALL DATUM(IFILE2,426,0,0,INT,REALL,CHARAC,IOUT,0,1)
      DO 110 I=1,LAYERS
      DO 105 II = 1,NTIN
      IPROMP = 1
      IF (II.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,426,1,1,INT,TIN(II),CHARAC,IOUT,II,IPROMP)
  105 CONTINUE
C
      CALL FINDZ(NTIN,ITIN,TIN,I5,THICK(1,I),S)
      REWIND IFILE2
110   CONTINUE
      GO TO 120
C
C     LAYER THICKNESSES FROM FUNCTION. . .
C
115   DO 117 I = 1,LAYERS
      READ(5,20) TH1,TH2,TH3,TH4,TH5
      DO 116 II = 1,I5
      SARC = S(II) - S(1)
      THICK(II,I) = TH1 + TH2*SARC**TH3 + TH4*SARC**TH5
116   CONTINUE
117   CONTINUE
C
120   WRITE (IFILE4,50)
      WRITE (IFILE4,60)(I,T(I),G(I),EX(I),EY(I),UXY(I),SM(I),ALPHA1(I),
     1 ALPHA2(I),I=1,LAYERS)
      CALL GASP(THICK(1,1),I5,1,ITHK(ISEG))
      IF (NTYPET.EQ.0) GO TO 170
      REWIND IFILE3
      CALL DATUM(IFILE3,608,1,1,INT,REALL,ANSOUT,IOUT,0,1)
      IF (ANSL1('N',ANSOUT,INFILE)) GO TO 170
      WRITE(IFILE4,130)
130   FORMAT(//77H  THICKNESS DISTRIBUTION OF LAYERS, STARTING WITH INNE
     1R LAYER AS NUMBER 1    )
      WRITE (IFILE4,140)
140   FORMAT(//129H POINT    T(1)      T(2)      T(3)      T(4)      T(5
     1)      T(6)      T(7)      T(8)      T(9)      T(10)     T(11)    
     2T(12)     ) 
      DO 150 I=1,I5
150   WRITE (IFILE4,160)I,(THICK(I,JJ),JJ=1,LAYERS)
160   FORMAT(I5,1P12E10.2) 
170   CONTINUE
      IF (NTYPET.EQ.0) GO TO 190
      DO 180 I=1,LAYERS
180   T(I) = THICK(K,I)
190   CONTINUE
      BI=0.5*T(1)
      CB33=0.0
      C33=0.0
      DO 200 I=1,LAYERS
      UYX(I) = 0.0
      IF (EX(I).NE.0.0) UYX(I) = EY(I)*UXY(I)/EX(I)
      U1(I)=1.0-UXY(I)*UYX(I)
      CI33=G(I)*T(I)
      CB33 = CB33 + BI*CI33
      C33=C33+CI33
      IF (I.LT.LAYERS) BI = BI + 0.5*(T(I) + T(I+1))
200   CONTINUE
      BI=0.5*T(1)
      C11=0.0
      C12=0.0
      C14=0.0
      C15=0.0
      C22=0.0
      C24=0.0
      C25=0.0
      C36 = 0.0
      C44=0.0
      C45=0.0
      C55=0.0
      C66=0.0
      SMPA = 0.0
      DO 230 I=1,LAYERS
      SMPA = SMPA + SM(I)*T(I)
      HI=Z-BI
      HI2=HI*HI
      CI11=EX(I)*T(I)/U1(I)
      CI22=EY(I)*T(I)/U1(I)
      CI12 = UYX(I)*CI11
      IF ((CI11*CI22).GE.(CI12**2)) GO TO 220
      WRITE (6,210)I
210   FORMAT(//40H WALL MATERIAL PROPERTIES FOR LAYER NO.  I3,14H INCONS
     1ISTENT.  /45H EX(I)/EY(I) MUST BE GREATER THAN UXY(I)**2     //)
      CALL ERREX
220   CONTINUE
      CI14=HI*CI11
      CI15=HI*CI12
      CI25=HI*CI22
      CI33=G(I)*T(I)
      RT=T(I)**2/12.
      CI44=CI11*RT
      CI45=CI12*RT
      CI55=CI22*RT
      CI66=    CI33*RT
      C11=C11+CI11
      C12=C12+CI12
      C14=C14+CI14
      C15=C15+CI15
      C22=C22+CI22
      C25=C25+CI25
      C36 = C36 - HI*CI33
      C44=C44+CI44+HI2*CI11
      C45=C45+CI45+HI2*CI12
      C55=C55+CI55+HI2*CI22
      C66=C66+CI66+    HI2*CI33
      IF (I.LT.LAYERS) BI = BI +0.5*(T(I) +T(I+1))
230   CONTINUE
      C24=C15
240    CONTINUE
C     THERMAL LOADS FOR KTH POINT OF IITH SEGMENT,LAYERED ORTHOTROPIC
      THERM(K,1) = 0.0
      THERM(K,2) = 0.0
      THERM(K,3) = 0.0
      THERM(K,4) = 0.0
      BB = 0.0
      NUM = 25
      DO 270 I=1,LAYERS
      I1 = I - 1
      IF (I1.EQ.0) GO TO 250
      BB = BB + T(I1)
250   CONTINUE
      Z1 = -(Z      -BB)
      Z2 = - (Z - BB - T(I))
      NTSGD = 0
      TIO(K,1) = FUNCT(T1,T2,T3,Z1)
      TIO(K,2) = FUNCT(T1,T2,T3,Z2)
260   CONTINUE
      CALL FINDTH(Z1,Z2,T1,T2,T3,NUM,TN1)
      NTSGD = 1
      CALL FINDTH(Z1,Z2,T1,T2,T3,NUM,TM1)
      E11 = EX(I)/U1(I)
      E22 = EY(I)/U1(I)
      E12 = UYX(I)*EX(I)/U1(I)
C     THERMAL STRESS RESULTANTS AND MOMENT RESULTANTS
      ABAR1 = -ALPHA1(I)*E11 - ALPHA2(I)*E12
      ABAR2 = -ALPHA1(I)*E12 - ALPHA2(I)*E22
      THERM(K,1) = THERM(K,1) + TN1*ABAR1
      THERM(K,2) = THERM(K,2) + TN1*ABAR2
      THERM(K,3) = THERM(K,3) - TM1*ABAR1
      THERM(K,4) = THERM(K,4) - TM1*ABAR2
270   CONTINUE
      IF (J.GT.1) GO TO 290
280   CONTINUE
      IF (NRS.EQ.0) RETURN
      TD=BI+0.5*T(LAYERS)
      CALL CFB1(K,RAD,NWALL,Z,TD,I5,S,C,RADD)
290   CONTINUE
      RETURN
      END
C=DECK      CFB6
      SUBROUTINE CFB6(I,RAD,NWALL,Z,I5,S,C,RADD)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C        SUBROUTINE FOR SHELLS WITH CORRUGATED SKIN.
C
      DIMENSION RAD(100),S(100),C(I5,14),RADD(100)
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/SHMASS/SMPA
      COMMON/PRMFIL/IFILE,IFILE2,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
      COMMON/INDAT/INFILE
      CHARACTER*4 ANSOUT,CHARAC
      LOGICAL ANSL1
10    FORMAT (6E12.8) 
20    FORMAT (/23H MODULUS OF ELASTICITY=E15.8,5X,14HPOISSON RATIO=E15.8
     1//3H C=E15.8,5X,2HH=E15.8,5X,2HD=E15.8,5X,2HT=E15.8,5X,2HB=E15.8)
30    FORMAT (/46H ANALYSIS IS FOR A SHELL WITH CORRUGATED SKIN.)
C
C
      IF (I.NE.1) GO TO 40
      WRITE (IFILE4,30)
      CALL DATUM(IFILE2,598,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,600,1,1,INT,E,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,602,1,1,INT,U,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,604,1,2,INT,ANRS,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,606,1,2,INT,SMPAD,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,607,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,608,1,1,INT,CT,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,610,1,2,INT,CB,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,612,1,1,INT,CC,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,614,1,1,INT,CD,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,616,1,1,INT,CH,CHARAC,IOUT,0,1)
C
C     E=MODULUS OF ELASTICITY OF CORRUGATED SKIN
C     U=POISSON'S RATIO OF CORRUGATED SKIN
C     ANRS=INDICATOR OF WALL STIFFENING
C            IF 0 ... NO STRINGERS AND/OR RINGS
C            IF 1 ... STIFFENED BY STRINGERS AND/OR RINGS
C     SMPA = SHELL MASS PER UNIT AREA
C     CC=WIDTH OF TOP OF CORRUGATION (AT MIDDLE SURFACE)
C     CH=TOTAL OVER-ALL HEIGHT OF CORRUGATION
C     CD=WIDTH OF TROUGH BETWEEN CORRUGATIONS (AT MIDDLE SURFACE)
C     CT=THICKNESS OF CORRUGATED SHEET
C     CB=CENTERLINE-TO-CENTERLINE SPACING OF CORRUGATIONS
C     Z = DISTANCE FROM SHELL INNER SURFACE TO REFERENCE SURFACE
C
      WRITE (IFILE4,20)E,U,CC,CH,CD,CT,CB
40    NRS = ANRS
      BE=(CB-CC-CD)/(2.*(CH-CT))
      ALP=SQRT(1.+BE**2)
      F=CC-CT*(ALP+BE)
      G=CD-CT*(ALP+BE)
      TX=2.*CT/CB*(0.5*(F+G)+ALP*CH+BE*CT)
      EP=CT*(0.5*CT*(G-F)+CH*(F+BE*CT+ALP*CH))/(CB*TX)
      XI=CT/CB*((ALP*CH**3+CT**2*(0.5*(F+G)+BE*CT))/6.+2.*ALP*CH*(0.5*CH
     1-EP)**2+G*(EP-0.5*CT)**2+F*(CH-EP-0.5*CT)
     2**2+BE*CT*((EP-2.*CT/3.)**
     32+(CH-EP-2.*CT/3.)**2))
      YI=CT**3*CB/(12.*(1.-U**2)*(ALP*2.*(CH-CT)+CC+CD))
      CTB=CT*CB/(2.*ALP*(CH-CT)+CC+CD)
      XJ = (CT**3/6.)*(ALP/BE +(CC+CD)*(1.-ALP/BE)/CB)
      DD = 0.5*CH - Z
      C11=E*TX
      C12=0.0
      C14=0.0
      C15=0.0
      C22=0.0
      C24=0.0
      C25=0.0
      C33=CTB*E/(2.*(1.+U))
      C36 = 0.0
      C44=E*XI
      C45=0.
      C55=E*YI
      C66 = 0.25*E*XJ/(2.*(1.+U))
      C14 = - DD*C11
      C15 = - DD*C12
      C24 = C15
      C25 = - DD*C22
      C36 = DD*C33
      C44 = C44 + DD*DD*C11
      C45 = 0.0
      C55 = C55 + DD*DD*C22
      C66 = C66 + DD*DD*C33
      SMPA = SMPAD
50    IF (NRS.EQ.0) RETURN
      TD=CH
      REWIND IFILE2
      CALL CFB1(I,RAD,NWALL,Z,TD,I5,S,C,RADD)
      RETURN
      END
C=DECK      CFB7
      SUBROUTINE CFB7(I,RAD,NWALL,Z,SS,I5,ISEG,TIO,SSS,CCC,RADD)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C     SUBROUTINE FOR ONE PLANE AND ONE CORRUGATED SKIN
C
      DIMENSION RAD(100),SS(100),THICK(100),RADD(100)
      COMMON/SHEL/ISHL(95),IWAL(95),ITHK(95),IARC(95),ILOAD(95)
      COMMON/WALPRP/EA(95),EB(95),GAB(95),UAB(95),ALPH1(95),ALPH2(95)
      DIMENSION TIO(I5,2),SSS(100),CCC(I5,14)
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/SHMASS/SMPA
      COMMON/KCHNG/NNWALL(95),CCH(95)
      COMMON/AXIALZ/ZAX(100)
      COMMON/SFLAG/MONOQ
      COMMON/PRMFIL/IFILE,IFILE2,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
      COMMON/INDAT/INFILE
      CHARACTER*4 ANSOUT,CHARAC
      LOGICAL ANSL1
5     FORMAT(10I6) 
10    FORMAT (6E12.8) 
20    FORMAT (/23H MODULUS OF ELASTICITY=E15.8,5X,14HPOISSON RATIO=E15.8
     1//3H C=E15.8,5X,2HH=E15.8,5X,2HD=E15.8,5X,2HT=E15.8,5X,2HB=E15.8  
     2//5H PHI=E15.8) 
30    FORMAT (/29H PROPERTIES OF PLANE SKIN ARE)
40    FORMAT (/34H PROPERTIES OF CORRUGATED SKIN ARE)
50    FORMAT (/23H MODULUS OF ELASTICITY=E15.8,5X,14HPOISSON RATIO=E15.8
     1//11H THICKNESS=E15.8)
60    FORMAT (/22H EXTERNAL CORRUGATIONS)
70    FORMAT (/22H INTERNAL CORRUGATIONS)
80    FORMAT (/64H ANALYSIS IS FOR A SHELL WITH ONE PLANE AND ONE CORRUG
     1ATED SKIN.) 
C
C
      IF (I.NE.1.AND.NTYPET.EQ.0) GO TO 130
      IF (I.NE.1) GO TO 100
      WRITE (IFILE4,80)
      CALL DATUM(IFILE2,617,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,618,1,1,INT,E,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,620,1,1,INT,U,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,622,1,2,INT,ANRS,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,624,1,2,INT,SMPAD,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,625,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,626,1,1,INT,CT,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,628,1,2,INT,CB,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,630,1,1,INT,CC,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,632,1,1,INT,CD,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,634,1,1,INT,CH,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,635,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,636,1,1,INT,ES,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,638,1,1,INT,US,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,640,1,1,INT,TS,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,642,1,2,NTYPET,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,644,1,2,INT,FIBOND,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,646,1,1,INOUT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,648,1,2,INT,PHI,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,650,1,2,INT,BAXIAL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,652,1,2,INT,BHOOP,CHARAC,IOUT,0,1)
C
      ANC = INOUT
      IF(BAXIAL.EQ.0.) BAXIAL = 1.
      IF(BHOOP .EQ.0.) BHOOP  = 1.
C
C     E=MODULUS OF ELASTICITY OF CORRUGATED SKIN
C     U=POISSON'S RATIO OF CORRUGATED SKIN
C     ANRS=INDICATOR OF WALL STIFFENING
C            IF 0 ... NO STRINGERS AND/OR RINGS
C            IF 1 ... STIFFENED BY STRINGERS AND/OR RINGS
C     SMPA = SHELL MASS PER UNIT AREA
C     BAXIAL = KNOCKDOWN FACTOR FOR AXIAL BENDING STIFFNESS C44 WHICH
C              DEPENDS ON THE DISCRETE RING SPACING.
C     BHOOP = KNOCKUP FACTOR FOR HOOP BENDING STIFFNESS, C55
C
C
C     CC=WIDTH OF TOP OF CORRUGATION (AT MIDDLE SURFACE)
C     CH=TOTAL OVER-ALL HEIGHT OF CORRUGATION
C     CD=WIDTH OF TROUGH BETWEEN CORRUGATIONS (AT MIDDLE SURFACE)
C     CT=THICKNESS OF CORRUGATED SHEET
C     CB=CENTERLINE-TO-CENTERLINE SPACING OF CORRUGATIONS
C     PHI=REDUCTION FACTOR FOR TORSIONAL STIFFNESS...SOMEWHERE BETWEEN
C                       0.25 AND 0.85
C
      WRITE (IFILE4,40)
      WRITE (IFILE4,20)E,U,CC,CH,CD,CT,CB,PHI
      CCH(ISEG) = CH
      NRS=ANRS
      BE=(CB-CC-CD)/(2.*(CH-CT))
      ALP=SQRT(1.+BE**2)
      F=CC-CT*(ALP+BE)
      G=CD-CT*(ALP+BE)
      TX=2.*CT/CB*(0.5*(F+G)+ALP*CH+BE*CT)
      EP=CT*(0.5*CT*(G-F)+CH*(F+BE*CT+ALP*CH))/(CB*TX)
      XI=CT/CB*((ALP*CH**3+CT**2*(0.5*(F+G)+BE*CT))/6.+2.*ALP*CH*(0.5*CH
     1-EP)**2+G*(EP-0.5*CT)**2+F*(CH-EP-0.5*CT)
     2**2+BE*CT*((EP-2.*CT/3.)**
     32+(CH-EP-2.*CT/3.)**2))
      YI=CT**3*CB/(12.*(1.-U**2)*(ALP*2.*(CH-CT)+CC+CD))
      CTB=CT*CB/(2.*ALP*(CH-CT)+CC+CD)
      IF (BE.NE.0.0) THEN
         XJ = (CT**3/6.)*(ALP/BE +(CC+CD)*(1.-ALP/BE)/CB)
      ELSE
         XJ =  CT**3/6.
      ENDIF
C
C     ES = MODULUS OF SMOOTH SKIN
C     POISSON'S RATION OF SMOOTH SKIN
C     THICKNESS OF SMOOTH SKIN
C     ANC=INDICATOR FOR POSITION OF CORRUGATION
C            IF 0 ... INSIDE CORRUGATION
C            IF 1 ... OUTSIDE CORRUGATION
C    NTYPET = THICKNESS VARIATION INDICATOR. . .
C                       0 = CONSTANT THICKNESS SKIN
C                       1 = VARIABLE THICKNESS SKIN, INPUT DATA READ
C                           IN FROM SUBROUTINE GETZ
C     FIBOND = MANUFACTURING TECHNIQUE INDICATOR - -
C                  0 = SKIN RIVETED TO CORRUGATIONS
C                  1 = SKIN WELDBONDED TO CORRUGATIONS
C     Z = DISTANCE FROM SHELL INNER SURFACE TO REFERENCE SURFACE
C     IF CORRUGATION INTERNAL, Z= DISTANCE FROM CROWN OF CORRUGATION
C     TO REFERENCE SURFACE.
C
      EA(ISEG) = ES
      EB(ISEG) = ES
      UAB(ISEG)= US
      GAB(ISEG)= .5*ES/(1.+US)
      ALPH1(ISEG) = 0.
      ALPH2(ISEG) = 0.
C
      IBOND = FIBOND
      LTYPET = 3
      IF (NTYPET.EQ.1) LTYPET = 1
      DO 90 II=1,I5
      TIO(II,1) = 0.0
      TIO(II,2) = 0.0
90    THICK(II) = TS
      IF (NTYPET.NE.1) GO TO 95
      IPRM(1) = 658
      IPRM(2) = 659
      CALL GETZ(LTYPET,I5,SS,THICK,RAD,RADD,ZAX)
   95 CONTINUE
      CALL GASP(THICK,I5,1,ITHK(ISEG))
      WRITE (IFILE4,30)
100   TS = THICK(I)
      IF (I.NE.1) GO TO 105
      WRITE (IFILE4,50)ES,US,TS
      IF (NTYPET.EQ.0) GO TO 105
      DO 104 II = 1,I5
      WRITE(IFILE4,103) II,SS(II),THICK(II)
103   FORMAT(10H STATION = I3,14H, ARC LENGTH=  1PE14.4,
     113H, THICKNESS =  1PE14.4 )
104   CONTINUE
105   CONTINUE
      V=E/(2.*(1.+U))
      GS=ES/(2.*(1.+US))
      S=TS
      TST = 0.5*V*CTB*(CH+TS)/(GS*S+V*CTB)
      IF (ANC) 120,120,110
110   IF (I.EQ.1) WRITE(IFILE4,60)
      E1=EP-TST+0.5*S
      E2=TST
      DD = TST + 0.5*S - Z
      GO TO 130
120   IF (I.EQ.1) WRITE(IFILE4,70)
      E1=-EP+TST-0.5*S
      E2=-TST
      DD = - TST +0.5*S - Z + CH
130   CONTINUE
      A=0.5*(CH-CT/2.)*(CC+CB-CD)
      C=ES*S/(1.-US**2)
      D=ES*S**3/(12.*(1.-US**2))
      IF (IBOND.EQ.0) GO TO 135
      DSKIN = D
      DPLUS = ES*(S+CT)**3/(12.*(1.-US**2))
      FL1 = CB - CD
      FL2 = CD
      D = (FL1 +FL2)/(FL1/DSKIN  + FL2/DPLUS)
135   CONTINUE
      C11=E*TX+C
      C12=US*C
      CB14 = - E1*E*TX + E2*C
      CB15 = US*E2*C
      CB24 = CB15
      C22 = C
      CB25 = E2*C
      C33=GS*S+V*CTB
      C44=D+E*XI+E*TX*E1**2+C*E2**2
      C45 = US*D + E2**2*C*US
      C55 = D*BHOOP + E2**2*C + E*YI
      C66=(V*XJ+GS*S**3/3.+PHI*4.*A**2/(CB*((CB-CD)/(GS*S)+(CC+ALP*2.
     1*(CH-CT))/(V*CT))))/4.
C
C     SHIFT REFERENCE SURFACE FROM SHEAR CENTER TO POINT A DISTANCE Z
C     FROM THE SHELL INNER SURFACE.
      C14 = CB14 - DD*C11
      C15 = CB15 - DD*C12
      C24 = C15
      C25 = CB25 - DD*C22
      C36 = DD*C33
      C44 = C44 - 2.*DD*CB14 + DD*DD*C11
      C45 = C45 - DD*CB15 - DD*CB24 + DD*DD*C12
      C55 = C55 - 2.*DD*CB25 + DD*DD*C22
      C66 = C66 + DD*DD*C33
      SMPA = SMPAD
      TD=CH+S
140   IF (NRS.EQ.0) RETURN
C     ADD SMEARED RINGS AND STRINGERS
      REWIND IFILE2
      CALL CFB1(I,RAD,NWALL,Z,TD,I5,SSS,CCC,RADD)
      RETURN
      END
C=DECK      CFB8
      SUBROUTINE CFB8(I,RAD,NWALL,Z,I5,THERM,T1,T2,T3,TEMP,IJ,S,C,RADD)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C     SUBROUTINE FOR SHELLS WITH TEMPERATURE-DEPENDENT WALL PROPERTIES.
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/SHMASS/ SMPA
      DIMENSION SM(5),T(5),HEAT(5,20),FOUT(16)
      DIMENSION G(20,5),EX(20,5),EY(20,5),UXY(20,5),A1(20,5),A2(20,5)
      DIMENSION RAD(100),NPOINT( 5),THERM(I5,4)
      DIMENSION S(100),C(I5,14),RADD(100)
      COMMON/AXIALZ/ZAXIAL(100)
      COMMON/PRMFIL/IFILE,IFILE2,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
      COMMON/INDAT/INFILE
      CHARACTER*4 ANSOUT,CHARAC
      LOGICAL ANSL1
      DIMENSION ITIN(50),THICK(100, 5),TIN(50)
10    FORMAT(10I6) 
20    FORMAT(6E12.8)
30    FORMAT(/50H ANALYSIS FOR LAYERED SHELL WITH                      )
40    FORMAT( 50H TEMPERATURE-DEPENDENT MATERIAL PROPERTIES.          /)
50    FORMAT(7E17.8)
      IF (IJ.GT.1) GO TO 190
      IF (I.NE.1) GO TO 160
      WRITE (IFILE4,30)
      WRITE (IFILE4,40)
C
C     WRAPS = NO.OF LAYERS
C     ANRS = 0 . . NO STRINGERS AND RINGS
C     ANRS = 1  STRINGERS AND RINGS
C     TYPET = 0  CONSTANT LAYER THICKNESSES
C     TYPET = 1 VARIABLE LAYER THICKNESSES
      CALL DATUM(IFILE2,430,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,431,1,1,INT,WRAPS,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,432,1,2,INT,ANRS,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,433,1,2,INT,TYPET,CHARAC,IOUT,0,1)
C
      TEMP = 1.0
      LAYERS = WRAPS
      IF (LAYERS.LT.1.OR.LAYERS.GT.20) THEN
        WRITE(6,*)' LAYERS NOT IN PERMISSIBLE RANGE 1 TO 20'
        CALL ERREX
      ENDIF
      DO 60 J=1,LAYERS
60    T(J) = 0.0
      NRS = ANRS
      NTYPET = TYPET
      IF (NTYPET.NE.0.AND.NTYPET.NE.1) THEN
        WRITE(6,*)' NTYPET NOT IN PERMISSIBLE RANGE 0 TO 1'
        CALL ERREX
      ENDIF
C     J = 1 . . INNERMOST LAYER. = LAYERS . . OUTERMOST LAYER
C     T = LAYER THICKNESS
      IF (NTYPET.NE.0) GO TO 65
      DO 63 J = 1,LAYERS
      IPROMP = 1
      IF (J.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,434,1,1,INT,T(J),CHARAC,IOUT,J,IPROMP)
   63 CONTINUE
   65 CONTINUE
      IF (NTYPET.EQ.0) GO TO 80
C     NTIN = NO. OF MERIDIONAL STATIONS FOR WHICH THICKNESS TO BE
C            SPECIFIED. POSITION (MESH PT TAG) OF THICKNESS CALLOUTS
C            DETERMINED IN STA
C     TIN =  THICKNESS OF GIVEN LAYER AT CALLOUT POINTS. THICKNESSES
C            AT INTERMEDIATE POINTS OBTAINED BY LINEAR INTERPOLATION
      CALL DATUM(IFILE2,435,1,2,NTIN,REALL,CHARAC,IOUT,0,1)
      IF (NTIN.LT.2.OR.NTIN.GT.20) THEN
        WRITE(6,*)' NTIN NOT IN PERMISSIBLE RANGE 2 TO 20'
        CALL ERREX
      ENDIF
      IPRM(2) = 436
      CALL STA(ITIN,ZAXIAL,RAD,RADD,S,NTIN,I5)
      CALL DATUM(IFILE2,439,0,0,INT,REALL,CHARAC,IOUT,0,1)
      DO 70 J=1,LAYERS
      DO 68 II = 1,NTIN
      IPROMP = 1
      IF (II.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,439,1,1,INT,TIN(II),CHARAC,IOUT,II,IPROMP)
   68 CONTINUE
      CALL FINDZ(NTIN,ITIN,TIN,I5,THICK(1,J),S)
      REWIND IFILE2
70    CONTINUE
80    CONTINUE
C
C     SM = LAYER MATERIAL MASS DENSITY
      DO 83 J = 1,LAYERS
      IPROMP = 1
      IF (J.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,440,1,1,INT,SM(J),CHARAC,IOUT,J,IPROMP)
   83 CONTINUE
      WRITE (IFILE4,90)(T(J),SM(J),J=1,LAYERS)
90    FORMAT( 19H LAYER THICKNESS =  E15.8, 22H LAYER MASS DENSITY =    
     1E15.8)
C
C     NPOINT(J) = NUMBER OF VALUES OF TEMPERATURE RISE FOR WHICH
C                 PROPERTIES GIVEN IN JTH LAYER
      DO 95 J = 1,LAYERS
      IPROMP = 1
      IF (J.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,441,1,2,NPOINT(J),REALL,CHARAC,IOUT,J,IPROMP)
   95 CONTINUE
      WRITE (IFILE4,100)LAYERS
100   FORMAT(/64H MATERIAL PROPERTIES AS FUNCTIONS OF TEMPERATURE FOR SH
     1ELL WITH      I2, 10H  LAYERS.     /) 
      WRITE (IFILE4,110)
110   FORMAT(6X,11HTEMPERATURE,9X,5HSHEAR,11X,6HYOUNGS,11X,6HYOUNGS,    
     111X,7HPOISSON,10X,7HTHERMAL,11X,7HTHERMAL       / 
     29X,4HRISE,12X,7HMODULUS,8X,10HMODULUS(X),8X,10HMODULUS(Y),
     38X,9HRATIO(XY),6X,11HEXP.COEF(X),6X,11HEXP.COEF(Y))
      IF (NTYPET.EQ.0) GO TO 160
      REWIND IFILE3
      CALL DATUM(IFILE3,609,1,1,INT,REALL,ANSOUT,IOUT,0,1)
      IF (ANSL1('N',ANSOUT,INFILE)) GO TO 160
      WRITE (IFILE4,120)
120   FORMAT(//77H  THICKNESS DISTRIBUTION OF LAYERS, STARTING WITH INNE
     1R LAYER AS NUMBER 1    ) 
      WRITE (IFILE4,130)
130   FORMAT(//129H POINT    T(1)      T(2)      T(3)      T(4)      T(5
     1)      T(6)      T(7)      T(8)      T(9)      T(10)     T(11)    
     2T(12)     )  
      DO 140 J=1,I5
140   WRITE (IFILE4,150)J,(THICK(J,JJ),JJ=1,LAYERS)
150   FORMAT(I5,1P12E10.2)
160   CONTINUE
      IF (NTYPET.EQ.0) GO TO 180
      DO 170 J=1,LAYERS
170   T(J) = THICK(I,J)
180   CONTINUE
      BI = 0.5*T(1)
      C11 = 0.0
      C12 = 0.0
      C14 = 0.0
      C15 = 0.0
      C22 = 0.0
      C24 = 0.0
      C25 = 0.0
      C33 = 0.0
      C36 = 0.0
      C44 = 0.0
      C45 = 0.0
      C55 = 0.0
      C66 = 0.0
      SMPA = 0.0
190   THERM(I,1) = 0.0
      THERM(I,2) = 0.0
      THERM(I,3) = 0.0
      THERM(I,4) = 0.0
      IF (IJ.GT.1) GO TO 240
      NUM = 25
      BB = 0.0
      IF (NTYPET.NE.0) REWIND IFILE2
      DO 230 J=1,LAYERS
      NPT = NPOINT(J)
      IF (I.GT.1) GO TO 210
C
C     HEAT = TEMPERATURES ABOVE ZERO-STRESS FOR WHICH WALL PROPERTIES
C            WILL BE SPECIFIED
C     G = SHEAR MODULUS
C     EX = MERIDIONAL MODULUS
C     EY = CIRCUMFERENTIAL MODULUS
C     UXY = POISSON,S RATIO
C     A1 = MERIDIONAL THERMAL COEFFICIENT OF EXPANSION
C     A2 = CIRCUMFERENTIAL THERMAL COEFFICIENT OF EXPANSION
      DO 191 K = 1,NPT
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,442,1,2,INT,HEAT(J,K),CHARAC,IOUT,K,IPROMP)
  191 CONTINUE
      DO 192 K = 1,NPT
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,443,1,1,INT,G(K,J),CHARAC,IOUT,K,IPROMP)
  192 CONTINUE
      DO 193 K = 1,NPT
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,444,1,1,INT,EX(K,J),CHARAC,IOUT,K,IPROMP)
  193 CONTINUE
      DO 194 K = 1,NPT
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,445,1,1,INT,EY(K,J),CHARAC,IOUT,K,IPROMP)
  194 CONTINUE
      DO 195 K = 1,NPT
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,446,1,2,INT,UXY(K,J),CHARAC,IOUT,K,IPROMP)
  195 CONTINUE
      DO 196 K = 1,NPT
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,447,1,1,INT,A1(K,J),CHARAC,IOUT,K,IPROMP)
  196 CONTINUE
      DO 197 K = 1,NPT
      IPROMP = 1
      IF (K.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,448,1,1,INT,A2(K,J),CHARAC,IOUT,K,IPROMP)
  197 CONTINUE
C
      WRITE (IFILE4,200)J
200   FORMAT(/61H WALL PROPERTIES AS FUNCTION OF TEMPERATURE FOR LAYER N
     1UMBER                                         I2/)
      WRITE (IFILE4,50)(HEAT(J,K),G(K,J),EX(K,J),EY(K,J),UXY(K,J),
     1 A1(K,J),A2(K,J), K=1,NPT)
210   CONTINUE
C     SMPA = MASS/AREA OF SHELL WALL
      SMPA = SMPA + SM(J)*T(J)
      J1 = J - 1
      IF (J1.EQ.0) GO TO 220
      BB = BB + T(J1)
220   CONTINUE
      Z1 = - (Z-BB)
      Z2 = - (Z-BB-T(J))
C
C     CONTRIBUTION OF JTH LAYER TO TOTAL STIFFNESS OF SHELL WALL IS
C     EVALUATED IN STIFF.
C
      CALL STIFF(Z1,Z2,T1,T2,T3,TEMP,NUM,NPT,FOUT,J,HEAT,G(1,J),EX(1,J),
     1EY(1,J),UXY(1,J),A1(1,J),A2(1,J))
      C11 = C11 + FOUT(4)
      C12 = C12 + FOUT(10)
      C14 = C14 - FOUT(5)
      C15 = C15 - FOUT(11)
      C22 = C22 + FOUT(7)
      C24 = C24 - FOUT(11)
      C25 = C25 - FOUT(8)
      C33 = C33 + FOUT(1)
      C36 = C36 + FOUT(2)
      C44 = C44 + FOUT(6)
      C45 = C45 + FOUT(12)
      C55 = C55 + FOUT(9)
      C66 = C66 + FOUT(3)
C     THERMAL STRESS AND MOMENT RESULTANTS
      THERM(I,1) = THERM(I,1) + FOUT(13)/TEMP
      THERM(I,2) = THERM(I,2) + FOUT(15)/TEMP
      THERM(I,3) = THERM(I,3) - FOUT(14)/TEMP
      THERM(I,4) = THERM(I,4) - FOUT(16)/TEMP
      IF (J.LT.LAYERS) BI = BI + 0.5*(T(J) + T(J+1))
      IF (J.LT.LAYERS) REWIND IFILE2
230   CONTINUE
      IF (NRS.EQ.0) RETURN
      TD = BI + 0.5 *T(LAYERS)
C     ADD SMEARED RINGS AND STRINGERS
      CALL CFB1(I,RAD,NWALL,Z,TD,I5,S,C,RADD)
240   CONTINUE
      RETURN
      END
C=DECK      INGRAT
      SUBROUTINE INGRAT(FMIN,FMAX,A,B,DZ,ZDZ,Z2DZ)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C     CALLED FROM CFB2, WHOSE TRACE IS WALLCF, READIT, MAIN
C     CALCULATES INTEGRALS THROUGH THICKNESS OF FUNCTIONS SUCH AS
C     1./(1. + Z/A),  Z/(1. + Z/A), AND Z**2/(1. + Z/A) IN WHICH
C     Z/A IS LESS THAN UNITY.
      F1(A,B,X) = (ALOG(1.+A*X) +B*(A*X*X/2. -A*A*X*X*X/3. +A*A*A*X*X*X*
     1X/4. -A**4*X**5/5.) )/A
      F2(A,B,X) = (A*X*X/2. -A*A*X*X*X/3. +A**3*X**4/4. -A**4*X**5/5.
     1+B*(A**3*X**3/3. -A**4*X**4/4. + A**5*X**5/5.)/A**2)/A
      F3(A,B,X) = X**3/3. + (1.-B/A)*(-A*X**4/4. +A*A*X**5/5. -A**3*X**6
     1/6.)
      IF (ABS(A).LT.0.001) GO TO 10
      DZ = F1(A,B,FMAX) - F1(A,B,FMIN)
      ZDZ = F2(A,B,FMAX) - F2(A,B,FMIN)
      Z2DZ = F3(A,B,FMAX) - F3(A,B,FMIN)
      GO TO 20
10    CONTINUE
      DZ = FMAX - FMIN + B*(FMAX**2 -FMIN**2)/2.
      ZDZ = (FMAX**2 -FMIN**2)/2. + B*(FMAX**3 - FMIN**3)/3.
      Z2DZ = (FMAX**3 -FMIN**3)/3. +B*(FMAX**4 -FMIN**4)/4.
20    CONTINUE
      RETURN
      END
C=DECK      STIFF
C     OVERLAY(2,0)
      SUBROUTINE STIFF(Z1,Z2,T1,T2,T3,TEMP,NUM,NPT,FOUT,J,HEAT,GG,EXX,
     1EYY,UXYI,ALPH1,ALPH2)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C     CALLED FROM CFB8,WHOSE TRACE IS WALLCF,READIT,MAIN.
C     THE STIFFNESS OF THE JTH LAYER OF AN ORTHOTROPIC SHELL WHOSE
C     MATERIAL PROPERTIES ARE TEMPERATURE-DEPENDENT IS CALCULATED. THIS
C     IS STIFFNESS RELATIVE TO THE SHELL REFERENCE SURFACE, OF COURSE.
C
      DIMENSION FOUT(16),     HEAT(5,20),TMAX(5),TMIN(5),TDEG(25)
      DIMENSION GG(20),EXX(20),EYY(20),UXYI(20),ALPH1(20),ALPH2(20)
      DIMENSION RATIO(25),G(25),E11(25),E22(25),E12(25),A1(25),A2(25)
      COMMON/ZGRAD/NTSGD
      NTSGD = 0
C     TEMPERATURE BOUNDS ESTABLISHED FROM TABLE.
      TMIN(J) = HEAT(J,1)
      TMAX(J) = HEAT(J,NPT)
      IC = NUM - 1
      T = Z2 - Z1
      DZ = T/FLOAT(IC)
      Z = Z1 - DZ
C     LOOP STARTS FOR OBTAINING VALUES AT STATIONS THRU THICKNESS
C     WHICH WILL SUBSEQUENTLY BE INTEGRATED.
      DO 30 K=1,NUM
      Z = Z + DZ
      TDEG(K) = FUNCT(T1,T2,T3,Z)*TEMP
      IF (TDEG(K).GT.TMAX(J).OR.TDEG(K).LT.TMIN(J)) GO TO 60
      DO 10 KK=1,NPT
      IF (TDEG(K).GE.HEAT(J,KK)) GO TO 10
      KS = KK - 1
      KS1 = KS + 1
      GO TO 20
10    CONTINUE
      KS = NPT
      KS1 = NPT - 1
20    CONTINUE
C
C     LINEAR INTERPOLATION FOR PROPERTIES AT KTH POINT IN JTH LAYER,
C     SINCE INPUT TABLES GIVE ONLY A FEW VALUES.
      RATIO(K) = (TDEG(K) - HEAT(J,KS))/(HEAT(J,KS1) - HEAT(J,KS))
      G(K) = GG(KS) + RATIO(K)*(GG(KS1) - GG(KS))
      EX = EXX(KS) + RATIO(K)*(EXX(KS1) - EXX(KS))
      EY = EYY(KS) + RATIO(K)*(EYY(KS1) - EYY(KS))
      UXY = UXYI(KS) + RATIO(K)*(UXYI(KS1) - UXYI(KS))
      ALPHA1 = ALPH1(KS) + RATIO(K)*(ALPH1(KS1) - ALPH1(KS))
      ALPHA2 = ALPH2(KS) + RATIO(K)*(ALPH2(KS1) - ALPH2(KS))
      UYX = EY*UXY/EX
      U1 = 1. - UXY*UYX
      E11(K) = EX/U1
      E22(K) = EY/U1
      E12(K) = UYX*EX/U1
      A1(K) = - ALPHA1*E11(K) - ALPHA2*E12(K)
      A2(K) = - ALPHA1*E12(K) - ALPHA2*E22(K)
30    CONTINUE
C
C     END OF FIRST LOOP. NOW START SIMPSON,S RULE INTEGRATION THRU
C     THICKNESS.
      DO 40 L=1,16
40    FOUT(L) = 0.0
      Z = Z1 - DZ
      DO 50 K=2,IC,2
      KM = K - 1
      KP = K + 1
      Z = Z + 2.*DZ
      ZM = Z - DZ
      ZP = Z + DZ
      GAVE = (G(KM) + 4.*G(K) + G(KP))*DZ/3.
      GAVEZ = (G(KM)*ZM + 4.*G(K)*Z + G(KP)*ZP)*DZ/3.
      GAVEZ2 = (G(KM)*ZM*ZM + 4.*G(K)*Z*Z + G(KP)*ZP*ZP)*DZ/3.
      E11AVE = (E11(KM) + 4.*E11(K) + E11(KP))*DZ/3.
      E11Z = (E11(KM)*ZM + 4.*E11(K)*Z + E11(KP)*ZP)*DZ/3.
      E11Z2 = (E11(KM)*ZM*ZM + 4.*E11(K)*Z*Z + E11(KP)*ZP*ZP)*DZ/3.
      E22AVE = (E22(KM) + 4.*E22(K) + E22(KP))*DZ/3.
      E22Z = (E22(KM)*ZM + 4.*E22(K)*Z + E22(KP)*ZP)*DZ/3.
      E22Z2 = (E22(KM)*ZM*ZM + 4.*E22(K)*Z*Z + E22(KP)*ZP*ZP)*DZ/3.
      E12AVE = (E12(KM) + 4.*E12(K) + E12(KP))*DZ/3.
      E12Z = (E12(KM)*ZM + 4.*E12(K)*Z + E12(KP)*ZP)*DZ/3.
      E12Z2 = (E12(KM)*ZM*ZM + 4.*E12(K)*Z*Z + E12(KP)*ZP*ZP)*DZ/3.
      A1M = A1(KM)*TDEG(KM)
      A2M = A2(KM)*TDEG(KM)
      A10 = A1(K )*TDEG(K )
      A20 = A2(K )*TDEG(K )
      A1P = A1(KP)*TDEG(KP)
      A2P = A2(KP)*TDEG(KP)
      TAVE1 = (A1M + 4.*A10 + A1P)*DZ/3.
      TAVE1Z = (A1M*ZM + 4.*A10*Z + A1P*ZP)*DZ/3.
      TAVE2 = (A2M + 4.*A20 + A2P)*DZ/3.
      TAVE2Z = (A2M*ZM + 4.*A20*Z + A2P*ZP)*DZ/3.
      FOUT(1) = FOUT(1) + GAVE
      FOUT(2) = FOUT(2) + GAVEZ
      FOUT(3) = FOUT(3) + GAVEZ2
      FOUT(4) = FOUT(4) + E11AVE
      FOUT(5) = FOUT(5) + E11Z
      FOUT(6) = FOUT(6) + E11Z2
      FOUT(7) = FOUT(7) + E22AVE
      FOUT(8) = FOUT(8) + E22Z
      FOUT(9) = FOUT(9) + E22Z2
      FOUT(10)= FOUT(10)+ E12AVE
      FOUT(11)= FOUT(11)+ E12Z
      FOUT(12)= FOUT(12)+ E12Z2
      FOUT(13) = FOUT(13) + TAVE1
      FOUT(14) = FOUT(14) + TAVE1Z
      FOUT(15) = FOUT(15) + TAVE2
      FOUT(16) = FOUT(16) + TAVE2Z
50    CONTINUE
C
C     INTEGRATION OF STIFFNESS PROPERTIES THRU JTH LAYER COMPLETED.
      GO TO 80
60    CONTINUE
      WRITE (6,70)
70    FORMAT(50H TEMPERATURE NOT IN RANGE OF TABULATED INPUT DATA.    /)
      CALL ERREX
80    CONTINUE
      RETURN
      END
C
C
C
C BEG APR 1992
C=DECK      CFB9
      SUBROUTINE CFB9(ISEG,IPOINT,RAD,NWALL,Z,I5,S,C,RADD,THERM,
     1                T1,T2,T3,TIO,THICK)
C+---------------------------------------------------------------------+
C+---------------------------------------------------------------------+
C
C  OBTAIN CONSTITUTIVE LAW FOR COMPOSITE WALL WITH LAMINAE AT
C  VARIOUS ANGLES WITH RESPECT TO THE MERIDIONAL COORDINATE.
C  SELECTED LAYERS MAY HAVE VARIABLE THICKNESS.
C
C
      COMMON/LAYER/MATL(90),LTYPE(1500),T(90),ANGLE(90),NLAYER(95)
      COMMON/MATER1/E1(20),E2(20),G(20),FNU(20),DENS(20)
      COMMON/MATERT/ALPHA1(20),ALPHA2(20),CURETP(20),EALLOW(5,20)
      COMMON/JWATCH/JOLD
      COMMON/CMAXS/CMAX
      COMMON/ABXXX/C11,C12,C13,C14,C15,C16,C22,C23,C24,C25,C26,C33,C34
      COMMON/ACXXX/ C35,C36,C44,C45,C46,C55,C56,C66
      COMMON/SHMASS/SMPA
      COMMON/COMNOD/NODES(100),NODALL(100),NODTOT
      COMMON/AXIALZ/ZAXIAL(100)
      COMMON/PRMFIL/IFILE,IFILE2,IOUT,IPRM(5)
      COMMON/PRMOUT/IFILE3,IFILE4
      COMMON/INDAT/INFILE
      COMMON/ILVARS/ILVAR(11,95),ITREF(95),NLVAR(95),ITIO(95)
      COMMON/ZGRAD/NTSGD
      CHARACTER*4 ANSOUT,CHARAC
      LOGICAL ANSL1
      DIMENSION ITIN(50),THICK(I5,11),TIN(50),ITSET(90),TIO(I5,2)
C
      DIMENSION CX(6,6),THERMX(6),CNEW(6,6),RAD(*),RADD(*),S(*)
      DIMENSION E1L(99),E2L(99),GL(99),ZETL(99),U12L(99),RHOL(99)
      DIMENSION A1L(99),A2L(99),A1L1(99),A1L2(99),A2L1(99),A2L2(99)
      DIMENSION TL(99),C(I5,14),THERM(I5,4),THMSFT(6),TTEMP(90)
C
      IF (ISEG.EQ.1.AND.IPOINT.EQ.1) THEN
         JOLD = 0
         CALL MOVER(0,0,MATL,1,90)
      ENDIF
      IF (IPOINT.NE.1) GO TO 100
      CALL INPROPW(NLAY,LTYPE,T,ANGLE,MATL,E1,E2,G,FNU,
     1           ALPHA1,ALPHA2,DENS,NRS,CURETP,EALLOW,JOLD)
C
      CALL NODOUT(NWALL,ISEG,I5,NODES,NODALL,NODTOT)
C
      IVAR = 1
      CALL MOVER(0,0,ILVAR(1,ISEG),1,11)
      CALL MOVER(0,0,ITSET,1,90)
      CALL DATUM(IFILE2,413,1,2,INT,TYPET,CHARAC,IOUT,0,1)
      NTYPET = TYPET
      IF (NTYPET.NE.0.AND.NTYPET.NE.1) THEN
        WRITE(6,*)' NTYPET NOT IN PERMISSIBLE RANGE 0 TO 1'
        CALL ERREX
      ENDIF
      IF (NTYPET.EQ.0) GO TO 28
   10 CONTINUE
C  THE THICKNESS OF ONE OR MORE OF THE LAYERS VARIES ALONG THE SEGMENT...
      IF (IVAR.EQ.1)
     1      CALL DATUM(IFILE2,428,0,0,INT,REALL,CHARAC,IOUT,0,1)
      CALL DATUM(IFILE2,429,1,2,ILVAR(IVAR,ISEG),REALL,CHARAC,
     1           IOUT,0,1)
      L = ILVAR(IVAR,ISEG)
      ITSET(L) = IVAR
      REWIND IFILE2
C     NTIN = NO. OF MERIDIONAL STATIONS FOR WHICH THICKNESS TO BE
C            SPECIFIED. POSITION (MESH PT TAG) OF THICKNESS CALLOUTS
C            DETERMINED IN STA
C     TIN =  THICKNESS OF GIVEN LAYER AT CALLOUT POINTS. THICKNESSES
C            AT INTERMEDIATE POINTS OBTAINED BY LINEAR INTERPOLATION
      CALL DATUM(IFILE2,422,1,2,NTIN,REALL,CHARAC,IOUT,0,1)
      IF (NTIN.LT.2.OR.NTIN.GT.20) THEN
        WRITE(6,*)' NTIN NOT IN PERMISSIBLE RANGE 2 TO 20'
        CALL ERREX
      ENDIF
      IPRM(2) = 423
      CALL STA(ITIN,ZAXIAL,RAD,RADD,S,NTIN,I5)
      DO 18 I = 1,NTIN
      IPROMP = 1
      IF (I.GT.1) IPROMP = 0
      CALL DATUM(IFILE2,426,1,1,INT,TIN(I),CHARAC,IOUT,I,IPROMP)
   18 CONTINUE
      CALL FINDZ(NTIN,ITIN,TIN,I5,THICK(1,IVAR),S)
      IVAR = IVAR + 1
      CALL DATUM(IFILE2,427,1,1,INT,REALL,ANSOUT,IOUT,0,1)
      IF (ANSL1('Y',ANSOUT,INFILE)) THEN
         IF (IVAR.GT.10) THEN
            WRITE(6,*)' MAX. OF 10 VARIABLE-THICKNESS LAYERS ALLOWED.'
            CALL ERREX
         ENDIF
         REWIND IFILE2
         GO TO 10
      ENDIF
      NLVAR(ISEG) = IVAR - 1
      NVAR = NLVAR(ISEG)
      INUMB = I5*NLVAR(ISEG)
      CALL GASP(THICK,INUMB,1,ITREF(ISEG))
      REWIND IFILE3
      CALL DATUM(IFILE3,608,1,1,INT,REALL,ANSOUT,IOUT,0,1)
      IF (ANSL1('N',ANSOUT,INFILE)) GO TO 170
      IF (NLAY.GT.10) THEN
         WRITE(IFILE4,130)
  130    FORMAT(//' THICKNESS DISTRIBUTION OF LAYERS WITH VARIABLE',
     1 ' THICKNESS. (LAYERS WITH CONSTANT THICKNESS NOT LISTED HERE)')
         WRITE (IFILE4,140) (ILVAR(I,ISEG),I=1,10)
  140 FORMAT(//' POINT    T(',I2,')     T(',I2,')     T(',I2,')     T(',
     1 I2,')     T(',I2,')     T(',I2,')     T('I2,')     T(',I2,
     1 ')     T(',I2,')     T(',I2,')')
         DO 150 I=1,I5
  150    WRITE (IFILE4,160)I,(THICK(I,JJ),JJ=1,NVAR)
  160    FORMAT(I5,1P10E10.2)
      ELSE
         WRITE(IFILE4,132)
  132    FORMAT(//' THICKNESS DISTRIBUTION OF LAYERS, STARTING WITH',
     1 ' LEFTMOST LAYER AS NUMBER 1')
         WRITE (IFILE4,142)
  142 FORMAT(//' POINT    T(1)      T(2)      T(3)      T(4)      T(',
     1'5)      T(6)      T(7)      T(8)      T(9)      T(10)')
         DO 168 I=1,I5
            DO 161 J = 1,NLAY
               JJ = J + JOLD
               K = LTYPE(JJ)
               TTEMP(J) = T(K)
               ITVAR = ITSET(J)
               IF (ITVAR.NE.0) TTEMP(J) = THICK(I,ITVAR)
  161       CONTINUE
            WRITE (IFILE4,162)I,(TTEMP(JJ),JJ=1,NLAY)
  162       FORMAT(I5,1P10E10.2)   
  168    CONTINUE
      ENDIF
170   CONTINUE
   28 CONTINUE
C
      DO 30 J = 1,NLAY
C
      JJ = J + JOLD
      K = LTYPE(JJ)
      M = MATL(K)
C
      E1L(J) = E1(M)
      E2L(J) = E2(M)
       GL(J) =  G(M)
      U12L(J)= FNU(M)
      RHOL(J)= DENS(M)
       TL(J) =  T(K)
      ZETL(J)= ANGLE(K)
      A1L(J) = ALPHA1(M)*(-CURETP(M))
      A2L(J) = ALPHA2(M)*(-CURETP(M))
   30 CONTINUE
C
      JOLD = JOLD + NLAY
      NLAYER(ISEG) = NLAY
  100 CONTINUE
      TD = 0.
      DO 110 J = 1,NLAY
         JJ = J + JOLD - NLAY
         K = LTYPE(JJ)
         TL(J) = T(K)
         IF (ITSET(J).NE.0) THEN
            IVAR = ITSET(J)
            TL(J) = THICK(IPOINT,IVAR)
         ENDIF
         TD = TD + TL(J)
  110 CONTINUE
      Z1 = -Z
      Z2 = Z1 + TD
C BEG OCT 2004
      NTSGD = 0
C END OCT 2004
      TIO(IPOINT,1) = FUNCT(T1,T2,T3,Z1)
      TIO(IPOINT,2) = FUNCT(T1,T2,T3,Z2)
      BI = 0.5*TL(1)
      TEMP1 = TIO(IPOINT,1)
      DT = TIO(IPOINT,2) - TEMP1
C BEG OCT 2004
C     IF (IPOINT.EQ.1) THEN
C        WRITE(IFILE4,'(A,/,A,2I5,/,A,/,1P,5E12.4,/,A,1P,3E12.4)')
C    1   ' **** IN CFB9, MORE THERMAL STUFF ****',
C    1   ' ISEG,IPOINT=',ISEG,IPOINT,
C    1   ' Z1,Z2,TIO(IPOINT,1),TIO(IPOINT,2),DT=',
C    1     Z1,Z2,TIO(IPOINT,1),TIO(IPOINT,2),DT,
C    1   ' T1,T2,T3=',T1,T2,T3
C     ENDIF
C END OCT 2004
      DO 120 J = 1,NLAY
         TEMP = TEMP1 + DT*BI/TD
         TEMPL= TEMP1 + DT*(BI-0.5*TL(J))/TD
         TEMPR= TEMP1 + DT*(BI+0.5*TL(J))/TD
         JJ = J + JOLD - NLAY
         K = LTYPE(JJ)
         M = MATL(K)
         A1L(J) = ALPHA1(M)*(-CURETP(M)+TEMP)
         A2L(J) = ALPHA2(M)*(-CURETP(M)+TEMP)
         A1L1(J)= ALPHA1(M)*(-CURETP(M)+TEMPL)
         A2L1(J)= ALPHA2(M)*(-CURETP(M)+TEMPL)
         A1L2(J)= ALPHA1(M)*(-CURETP(M)+TEMPR)
         A2L2(J)= ALPHA2(M)*(-CURETP(M)+TEMPR)
         IF (J.LT.NLAY) BI = BI + 0.5*(TL(J)+TL(J+1))
  120 CONTINUE
      ZREF = 0.
      CALL CFBL(NLAY,ZREF,E1L,E2L,GL,U12L,RHOL,TL,ZETL,CX,
     1     A1L,A2L,THERMX,TMS,TD,A1L1,A1L2,A2L1,A2L2)
      SMPA = TMS
      DZREF = Z - TD/2.
      CALL CSHIFT(CX,DZREF,CNEW,THERMX,THMSFT)
C
      C11 = CNEW(1,1)
      C12 = CNEW(1,2)
      C13 = 0.
      C14 = CNEW(1,4)
      C15 = CNEW(1,5)
      C16 = 0.
      C22 = CNEW(2,2)
      C23 = 0.
      C24 = CNEW(2,4)
      C25 = CNEW(2,5)
      C26 = 0.
      C33 = CNEW(3,3)
      C34 = 0.
      C35 = 0.
      C36 = CNEW(3,6)
      C44 = CNEW(4,4)
      C45 = CNEW(4,5)
      C46 = 0.
      C55 = CNEW(5,5)
      C56 = 0.
      C66 = CNEW(6,6)
C
      THERM(IPOINT,1) = -THERMX(1)
      THERM(IPOINT,2) = -THERMX(2)
      THERM(IPOINT,3) =  THMSFT(4)
      THERM(IPOINT,4) =  THMSFT(5)
C
      IF (NRS.NE.0) CALL CFB1(IPOINT,RAD,NWALL,Z,TD,I5,S,C,RADD)
C      
      RETURN
      END
